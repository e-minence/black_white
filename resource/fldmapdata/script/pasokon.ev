//======================================================================
//                pasokon.ev
//    スクリプトファイル：pasokon用
//======================================================================

  .text

  .include  "scr_seq_def.h"
  .include  "msg_pasokon.h"
  //.include  "pasokon.h"

  .include  "hyouka_scr_def.h"

//--------------------------------------------------------------
//               スクリプト本体
//--------------------------------------------------------------

//--------------------------------------------------------------
//               スクリプトテーブル
//--------------------------------------------------------------
_EVENT_DATA ev_pc
_EVENT_DATA_END


//--------------------------------------------------------------
//               スクリプト内スコープ変数
//--------------------------------------------------------------
// パソコン終了フラグ
DEFINE_LOCAL log_out 

/**************************************************************/
/*                                                            */
/*                                                            */
/*        ポケモンセンター　PC                                */
/*                                                            */
/*                                                            */
/**************************************************************/
EVENT_START ev_pc
{ 
  DEFINE_LOCAL select_pos
  DEFINE_LOCAL f_pcname
  DEFINE_LOCAL f_zukan_get

  // ローカル変数の初期化
  $log_out = FALSE
  _FLAG_CHECK( SYS_FLAG_PCNAME, f_pcname )
  _FLAG_CHECK( SYS_FLAG_ZUKAN_GET, f_zukan_get )

  // PC起動イベント
  _POKECEN_PC_ON()

  // 「パソコンの スイッチを いれた！」
  _PLAYER_NAME( 0 )
  _SYSWIN_MSG_DOWN( msg_pc_on_01 )
  _SYSWIN_CLOSE()

  WHILE $log_out == FALSE 
    // 「どこの パソコンに せつぞく …」
    _SYSWIN_MSG_DOWN( msg_pc_on_02 )
    // 選択ウィンドウ表示
    _BMPMENU_INIT_EX( 1, 1, 0, TRUE, select_pos )
    IF $f_pcname == FALSE THEN
      _BMPMENU_MAKE_LIST( msg_pc_window_01, msg_pc_window_01 )  // だれかのパソコン
    ELSE
      _BMPMENU_MAKE_LIST( msg_pc_window_02, msg_pc_window_02 )  // 開発者のパソコン
    ENDIF
    _PLAYER_NAME( 0 )
    _BMPMENU_MAKE_LIST( msg_pc_window_03, msg_pc_window_03 )  // 自分のパソコン
    IF $f_zukan_get == TRUE THEN
      _BMPMENU_MAKE_LIST( msg_pc_window_04, msg_pc_window_04 )  // はかせのパソコン
    ENDIF
    // @todo 殿堂入りしていなければ表示しない
    _BMPMENU_MAKE_LIST( msg_pc_window_05, msg_pc_window_05 )  // でんどういり
    _BMPMENU_MAKE_LIST( msg_pc_window_06, msg_pc_window_06 )  // スイッチをきる
    _BMPMENU_START_BREAKABLE()
    _SYSWIN_CLOSE()
    // 選択に応じた処理
    SWITCH $select_pos
    CASE msg_pc_window_01 THEN _CALL( pc_box )
    CASE msg_pc_window_02 THEN _CALL( pc_box )
    CASE msg_pc_window_03 THEN _CALL( pc_mail_box )
    CASE msg_pc_window_04 THEN _CALL( pc_zukan_check )
    CASE msg_pc_window_05 THEN _CALL( pc_dendouiri )
    CASE msg_pc_window_06 THEN $log_out = TRUE
    CASE EV_WIN_B_CANCEL  THEN $log_out = TRUE
    CASE EV_WIN_X_BREAK   THEN $log_out = TRUE
    ENDSWITCH
  ENDWHILE

  // PC停止イベント
  _POKECEN_PC_OFF()
}
EVENT_END 


/**************************************************************/
/*       ボックス処理                                         */
/**************************************************************/
pc_box:
{
  DEFINE_LOCAL exit
  DEFINE_LOCAL select_pos
  DEFINE_LOCAL box_end_mode

  // SE
  _SE_PLAY( SEQ_SE_PC_LOGIN )

  //「ポケモンの あずかりシステムを …」
  _SYSWIN_MSG_DOWN( msg_pc_box_01 ) 
  _SYSWIN_CLOSE()

  $exit = FALSE
  WHILE $exit == FALSE
    // 説明文表示用のウィンドウを表示
    _SYSWIN_MSG_DOWN( msg_exp_window )
    // 選択ウィンドウ表示
    _BMPMENU_INIT_EX( 1, 1, 0, TRUE, select_pos )
    _BMPMENU_MAKE_LIST_EX( msg_pc_box_window_01, msg_pc_box_02, msg_pc_box_window_01 )  // あずける
    _BMPMENU_MAKE_LIST_EX( msg_pc_box_window_02, msg_pc_box_03, msg_pc_box_window_02 )  // つれていく
    _BMPMENU_MAKE_LIST_EX( msg_pc_box_window_03, msg_pc_box_04, msg_pc_box_window_03 )  // せいりする
    _BMPMENU_MAKE_LIST_EX( msg_pc_box_window_06, msg_pc_box_07, msg_pc_box_window_06 )  // バトルボックス
    _BMPMENU_MAKE_LIST_EX( msg_pc_box_window_04, msg_pc_box_05, msg_pc_box_window_04 )  // もちもの
    _BMPMENU_MAKE_LIST_EX( msg_pc_box_window_05, msg_pc_box_06, msg_pc_box_window_05 )  // さようなら
    _BMPMENU_START_BREAKABLE() 
    _SYSWIN_CLOSE()
    //「さようなら」選択
    SWITCH $select_pos
    CASE msg_pc_box_window_05 THEN _RET
    CASE EV_WIN_B_CANCEL      THEN _RET
    CASE EV_WIN_X_BREAK       THEN $log_out = TRUE
                                   _RET
    ENDSWITCH
    // ボックス呼び出し
    _MAP_FADE_BLACK_OUT() // フェードアウト
    _MAP_FADE_END_CHECK() //
    SWITCH $select_pos
    CASE msg_pc_box_window_01 THEN _CALL_BOX_PROC( box_end_mode, BOX_MODE_AZUKERU )
    CASE msg_pc_box_window_02 THEN _CALL_BOX_PROC( box_end_mode, BOX_MODE_TURETEIKU )
    CASE msg_pc_box_window_03 THEN _CALL_BOX_PROC( box_end_mode, BOX_MODE_SEIRI )
    CASE msg_pc_box_window_06 THEN _CALL_BOX_PROC( box_end_mode, BOX_MODE_BATTLE )
    CASE msg_pc_box_window_04 THEN _CALL_BOX_PROC( box_end_mode, BOX_MODE_ITEM )
    ENDSWITCH 
    // 画面復帰
    _POKECEN_PC_RUN()           // パソコンアニメ再生
    _MAP_FADE_BLACK_IN()        // フェードイン
    _MAP_FADE_END_CHECK()       // フェードイン終了待ち
    // ボックス終了モードをチェック
    IF $box_end_mode == SCR_BOX_END_MODE_MENU THEN
      _SE_PLAY( SEQ_SE_PC_LOGIN ) // SE
    ELSE
      $exit    = TRUE             // パソコンを切る
      $log_out = TRUE             //
    ENDIF
  ENDWHILE
}
_RET

/**************************************************************/
/*       メールボックス処理                                   */
/**************************************************************/
pc_mail_box:
{
  DEFINE_LOCAL exit
  DEFINE_LOCAL select_pos 

  // SE
  _SE_PLAY( SEQ_SE_PC_LOGIN )

  //「○○の パコンに せつぞくした！」
  _PLAYER_NAME(0)
  _SYSWIN_MSG_DOWN( msg_pc_mail_01 ) 
  _AB_KEYWAIT()
  _SYSWIN_CLOSE()

  $exit = FALSE
  WHILE $exit == FALSE
    // 説明文表示用のウィンドウを表示
    _SYSWIN_MSG_DOWN( msg_exp_window )
    // 選択ウィンドウ表示
    _BMPMENU_INIT_EX( 1, 1, 0, TRUE, select_pos )
    _BMPMENU_MAKE_LIST_EX( msg_pc_mail_window_01, msg_pc_mail_02, msg_pc_mail_window_01 )  // メールボックス
    _BMPMENU_MAKE_LIST_EX( msg_pc_mail_window_02, msg_pc_mail_03, msg_pc_mail_window_02 )  // せつぞくを きる
    _BMPMENU_START_BREAKABLE()
    _SYSWIN_CLOSE()
    //「せつぞくを きる」選択
    SWITCH $select_pos
    CASE msg_pc_mail_window_02 THEN _RET
    CASE EV_WIN_B_CANCEL       THEN _RET
    CASE EV_WIN_X_BREAK        THEN $log_out = TRUE
                                    _RET
    ENDSWITCH
    // メールボックス呼び出し
    _MAP_FADE_BLACK_OUT()        // フェードアウト
    _MAP_FADE_END_CHECK()        //
    _CALL_MAILBOX_PROC()         // メールボックス画面へ
    _POKECEN_PC_RUN()            // パソコンアニメ再生
    _MAP_FADE_BLACK_IN()         // フェードイン
    _MAP_FADE_END_CHECK()        //
    _SE_PLAY( SEQ_SE_PC_LOGIN )  // SE
  ENDWHILE
}
_RET

/**************************************************************/
/*       博士の図鑑評価処理                                   */
/**************************************************************/
pc_zukan_check:
{
  // SE
  _SE_PLAY( SEQ_SE_PC_LOGIN )

  //「はかせの パソコンに せつぞくした！」
  _SYSWIN_MSG_DOWN( msg_pc_zukan_check_01 ) 
  _AB_KEYWAIT()
  _SYSWIN_CLOSE()
  _CHG_COMMON_SCR SCRID_ZUKAN_MACHINE

  // @todo 図鑑評価メッセージを表示
}
_RET

/**************************************************************/
/*       殿堂入り処理                                         */
/**************************************************************/
pc_dendouiri:
{
  // SE
  _SE_PLAY( SEQ_SE_PC_LOGIN )

  // @todo データが正常かどうかをチェック
  // @todo 殿堂入り画面の表示
}
_RET
