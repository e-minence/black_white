
  DEFINE_LOCAL  LWK_TRAINERID
  DEFINE_LOCAL  LWK_MSGID_START
  DEFINE_LOCAL  LWK_MSGID_AFTER
  DEFINE_LOCAL  LWK_MSGID_NG
  DEFINE_LOCAL  LWK_TRAINER_ITEM

//******************************************************************
//  会話からのトレーナー戦闘
//  LWK_TRAINERID  トレーナーID
//  SCWK_TEMP3  退避トレーナーID
//******************************************************************
EVENT_START ev_trainer_talk_battle
  _TALK_OBJ_START()
  
  //かくれみのアニメチェック
  _CALL sub_trainer_talk_battle_hide_pulloff
  
  //トレーナーID取得
  _TRAINER_ID_GET( LWK_TRAINERID )
  
  //すでに勝利しているトレーナーかチェックして、戦闘勝利後の会話へ
  _TRAINER_FLAG_CHECK( LWK_TRAINERID, SCWK_ANSWER )

  IF $SCWK_ANSWER == 1 THEN

    _TRAINER_SPTYPE_GET( LWK_TRAINERID, SCWK_ANSWER )

    IF $SCWK_ANSWER == SCR_TR_SPTYPE_RECOVER THEN
        _CALL sub_trainer_talk_after_recover
    ELSIF $SCWK_ANSWER == SCR_TR_SPTYPE_ITEM THEN
        _CALL sub_trainer_talk_after_item_after
    ELSE
        _CALL sub_trainer_talk_after_normal
    ENDIF

  ELSE
  {
    DEFINE_LOCAL  LWK_BTL_RULE
    DEFINE_LOCAL  LWK_BTL_MEMBER

    //トレーナータイプ取得(SCR_TR_BTL_RULE_〜）
    _TRAINER_BTL_RULE_GET( LWK_TRAINERID, LWK_BTL_RULE )
    //何匹戦えるポケモンが手持ちにいるか、取得
    _GET_PARTY_POKE_COUNT( LWK_BTL_MEMBER, POKECOUNT_MODE_BATTLE_ENABLE )

    IF $LWK_BTL_RULE == SCR_TR_BTL_RULE_DOUBLE && $LWK_BTL_MEMBER < 2 THEN
      //ダブルバトルで手持ちが足りない場合、NGメッセージへ
      _CALL sub_trainer_talk_2vs2_NG
    ELSIF $LWK_BTL_RULE == SCR_TR_BTL_RULE_TRIPLE && $LWK_BTL_MEMBER < 3 THEN
      //トリプルバトルで手持ちが足りない場合、NGメッセージへ
      _CALL sub_trainer_talk_3vs3_NG
    ELSIF $LWK_BTL_RULE == SCR_TR_BTL_RULE_ROTATION && $LWK_BTL_MEMBER < 3 THEN
      //ローテーションバトルで手持ちが足りない場合、NGメッセージへ
      _CALL sub_trainer_talk_3vs3_NG
    ELSE
      //それ以外の場合、戦闘へ
      _CALL sub_trainer_talk_battle

      //特殊トレーナーの場合、戦闘後メッセージ
      _TRAINER_SPTYPE_GET( LWK_TRAINERID, SCWK_ANSWER )
      IF $SCWK_ANSWER == SCR_TR_SPTYPE_RECOVER THEN
        _CALL sub_trainer_talk_after_recover
      ELSIF $SCWK_ANSWER == SCR_TR_SPTYPE_ITEM THEN
        _CALL sub_trainer_talk_after_item_before
      ENDIF
    ENDIF
  }
  ENDIF
  
EVENT_END

sub_trainer_talk_battle:
  _TRAINER_BGM_SET( LWK_TRAINERID ) //視線曲セット
  _TRAINER_TALK_TYPE_GET( LWK_MSGID_START, LWK_MSGID_AFTER, LWK_MSGID_NG )
  _TRAINER_MSG_SET( LWK_TRAINERID, LWK_MSGID_START )
  _BALLOONWIN_CLOSE()
  
  /* _BTL_SEARCHER_DIR_MV_SET */
  
  //戦闘呼び出し
  _TRAINER_BTL_SET( LWK_TRAINERID, 0 )
  _TRAINER_LOSE_CHECK( SCWK_ANSWER )

  IF $SCWK_ANSWER == SCR_BATTLE_RESULT_WIN THEN
    _TRAINER_WIN()
  ELSE
    _TRAINER_LOSE()
  ENDIF

  //トレーナーフラグセット
  _TRAINER_FLAG_SET( LWK_TRAINERID )
  _RET

//--------------------------------------------------------------------
//  2vs2が不可能な（手持ちが足りない）場合
//--------------------------------------------------------------------
sub_trainer_talk_2vs2_NG:
  _TRAINER_ID_GET( SCWK_ANSWER )
  _TRAINER_TALK_TYPE_GET( LWK_MSGID_START, LWK_MSGID_AFTER, LWK_MSGID_NG )
  _TRAINER_MSG_SET( LWK_TRAINERID, LWK_MSGID_NG )
  _LAST_KEYWAIT()
  _BALLOONWIN_CLOSE()
  _RET

//--------------------------------------------------------------------
//  3vs3が不可能な（手持ちが足りない）場合
//--------------------------------------------------------------------
sub_trainer_talk_3vs3_NG:
  _TRAINER_ID_GET( LWK_TRAINERID )
  _TRAINER_TALK_TYPE_GET( LWK_MSGID_START, LWK_MSGID_AFTER, LWK_MSGID_NG )
  _TRAINER_MSG_SET( LWK_TRAINERID, LWK_MSGID_NG )
  _LAST_KEYWAIT()
  _BALLOONWIN_CLOSE()
  _RET

//--------------------------------------------------------------------
//  戦闘勝利後の会話
//--------------------------------------------------------------------
ev_trainer_talk_after:
  /*
  //話しかけたトレーナーが再戦可能かチェック
  _REVENGE_TRAINER_SEARCH LWK_TRAINERID, SCWK_ANSWER
  IF $SCWK_ANSWER != 0 THEN
    $LWK_TRAINERID = $SCWK_ANSWER //再戦トレーナーIDセット
    _JUMP ev_trainer_revenge
  ENDIF
  _JUMP        sub_trainer_talk_after_normal
  */

//戦闘後メッセージ：通常トレーナー
sub_trainer_talk_after_normal:
  //メッセージを取得
  _TRAINER_TALK_TYPE_GET(  LWK_MSGID_START, LWK_MSGID_AFTER, LWK_MSGID_NG )
  _TRAINER_MSG_SET(  LWK_TRAINERID, LWK_MSGID_AFTER )
  _LAST_KEYWAIT()
  _BALLOONWIN_CLOSE()

  _RET
  
//戦闘後メッセージ：回復トレーナー
sub_trainer_talk_after_recover:
  _TRAINER_MSG_SET( LWK_TRAINERID, TRMSG_FIGHT_AFTER_RECOVER_BEFORE )
  _BALLOONWIN_CLOSE()

  _BLACK_OUT()
  _DISP_FADE_END_CHECK()

  _TEMOTI_POKEMON_KAIFUKU() //回復処理
	_ME_PLAY( SEQ_ME_ASA )
  _ME_WAIT()

  _BLACK_IN()
  _DISP_FADE_END_CHECK()

  _TRAINER_MSG_SET( LWK_TRAINERID, TRMSG_FIGHT_AFTER_RECOVER_AFTER )
  _LAST_KEYWAIT()
  _BALLOONWIN_CLOSE()

  _RET

//戦闘後メッセージ：アイテムトレーナー：初回
sub_trainer_talk_after_item_before:
  _TRAINER_MSG_SET( LWK_TRAINERID, TRMSG_FIGHT_AFTER_GIFTITEM_BEFORE )
  _AB_KEYWAIT_MARK()
  _BALLOONWIN_CLOSE()

  _TRAINER_ITEM_GET( LWK_TRAINERID, SCWK_PARAM0 )
  $SCWK_PARAM1 = 1
  _GLOBAL_CALL  SCRID_ITEM_EVENT_KEYWAIT

  _TRAINER_MSG_SET( LWK_TRAINERID, TRMSG_FIGHT_AFTER_GIFTITEM_AFTER )
  _LAST_KEYWAIT()
  _BALLOONWIN_CLOSE()
  _RET

//戦闘後メッセージ：アイテムトレーナー：２回目以降
sub_trainer_talk_after_item_after:
  _TRAINER_MSG_SET( LWK_TRAINERID, TRMSG_FIGHT_AFTER_GIFTITEM_AFTER )
  _LAST_KEYWAIT()
  _BALLOONWIN_CLOSE()
  _RET

//--------------------------------------------------------------------
//  再戦
//--------------------------------------------------------------------
/*
ev_trainer_revenge:
  _TRAINER_BGM_SET( LWK_TRAINERID )                   //視線曲セット
  _REVENGE_TRAINER_TALK_TYPE_GET( LWK_MSGID_START, LWK_MSGID_AFTER, LWK_MSGID_NG ) //メッセージを取得
  _TRAINER_MSG_SET( LWK_TRAINERID, LWK_MSGID_START )

  // _BTL_SEARCHER_DIR_MV_SET
  
  //戦闘呼び出し
  _TRAINER_BTL_SET( LWK_TRAINERID, 0 )
  _TRAINER_LOSE_CHECK( SCWK_ANSWER )

  IF $SCWK_ANSWER == SCR_BATTLE_RESULT_WIN THEN
    _TRAINER_WIN()
  ELSE
    _TRAINER_LOSE()
  ENDIF

  //トレーナーフラグセット
  _TRAINER_FLAG_SET( LWK_TRAINERID )
  _RET

*/


//--------------------------------------------------------------------
//かくれみのアニメ
//--------------------------------------------------------------------
sub_trainer_talk_battle_hide_pulloff:
{
  //かくれみのアニメチェック
  _MOVE_CODE_GET( SCWK_ANSWER, SCWK_TARGET_OBJID ) //戻り値取得ワーク、話しかけ対象OBJID
  
  SWITCH $SCWK_ANSWER
  CASE MV_HIDE_SNOW THEN
    _CALL sub_trainer_talk_battle_hide_pulloff_change
  CASE MV_HIDE_SAND THEN
    _CALL sub_trainer_talk_battle_hide_pulloff_change
  CASE MV_HIDE_GRND THEN
    _CALL sub_trainer_talk_battle_hide_pulloff_change
  CASE MV_HIDE_KUSA THEN
    _CALL sub_trainer_talk_battle_hide_pulloff_change
  ENDSWITCH //MOVE_CODE
}
_RET

sub_trainer_talk_battle_hide_pulloff_change:
{
  _OBJ_ANIME( SCWK_TARGET_OBJID, anm_trainer_talk_battle_hide_pulloff )
  _OBJ_ANIME_WAIT()
    
  //動作コードを書き換えないと、再度、土の中から出てくるアニメが入ってしまう！
  _PLAYER_DIR_GET( SCWK_ANSWER )
    
  SWITCH $SCWK_ANSWER
  CASE DIR_UP THEN
    _MOVE_CODE_CHANGE( SCWK_TARGET_OBJID, MV_DOWN )
  CASE DIR_DOWN THEN
    _MOVE_CODE_CHANGE( SCWK_TARGET_OBJID, MV_UP )
  CASE DIR_LEFT THEN
    _MOVE_CODE_CHANGE( SCWK_TARGET_OBJID, MV_RIGHT )
  CASE DIR_RIGHT THEN
    _MOVE_CODE_CHANGE( SCWK_TARGET_OBJID, MV_LEFT )
  ENDSWITCH //DIR
}
_RET

_ANIME_LABEL  anm_trainer_talk_battle_hide_pulloff
  _ANIME_DATA  AC_HIDE_PULLOFF,1
  _ANIME_DATA  ACMD_END,0

//******************************************************************
//          視線からのトレーナー戦闘            
//
//  LWK_TRAINERID  トレーナーID_0
//******************************************************************
EVENT_START ev_trainer_move_battle
  //視線移動スクリプトコマンドの中で
  //対象のOBJの動作ポーズを解除するようにしている！
  //_PROGRAM      TR_BGM_SET
  
  //トレーナータイプ取得(シングル、ダブル、タッグ)
  _EYE_TRAINER_MOVE_TYPE_GET( SCWK_ANSWER )

  IF $SCWK_ANSWER == SCR_EYE_TR_TYPE_SINGLE THEN 
    _CALL ev_trainer_type_single
  ELSIF $SCWK_ANSWER == SCR_EYE_TR_TYPE_DOUBLE THEN
    _CALL ev_trainer_type_double
  ELSIF $SCWK_ANSWER == SCR_EYE_TR_TYPE_TAG THEN
    _CALL ev_trainer_type_tag
  ENDIF

  //特殊トレーナーの場合、戦闘後メッセージ
  _EYE_TRAINER_ID_GET( SCR_EYE_TR_0, LWK_TRAINERID )
  _TRAINER_SPTYPE_GET( LWK_TRAINERID, SCWK_ANSWER )
  IF $SCWK_ANSWER == SCR_TR_SPTYPE_RECOVER THEN
    _CALL sub_trainer_talk_after_recover
  ELSIF $SCWK_ANSWER == SCR_TR_SPTYPE_ITEM THEN
    _CALL sub_trainer_talk_after_item_before
  ENDIF

EVENT_END

//--------------------------------------------------------------------
//シングル
//--------------------------------------------------------------------
ev_trainer_type_single:
{
  DEFINE_LOCAL  LWK_TRID

  _EYE_TRAINER_ID_GET( SCR_EYE_TR_0, LWK_TRID )

  //視線曲セット
  _TRAINER_BGM_SET( LWK_TRID )
  
  _EYE_TRAINER_MOVE_SET( SCR_EYE_TR_0 )        //トレーナー移動処理呼び出し
  _EYE_TRAINER_MOVE_SINGLE( SCR_EYE_TR_0 )     //移動終了待ち

  //移動終了後、会話へ
  _TRAINER_MSG_SET(  LWK_TRID,TRMSG_FIGHT_START )
  _BALLOONWIN_CLOSE()
  
  _TRAINER_BTL_SET( LWK_TRID, 0 )
  _TRAINER_LOSE_CHECK( SCWK_ANSWER )

  IF $SCWK_ANSWER == SCR_BATTLE_RESULT_WIN THEN
    _TRAINER_WIN()
  ELSE
    _TRAINER_LOSE()
  ENDIF
  
  //トレーナーフラグセット
  _TRAINER_FLAG_SET(  LWK_TRID )
  _RET
}

//--------------------------------------------------------------------
//ダブル
//--------------------------------------------------------------------
ev_trainer_type_double:
{
  DEFINE_LOCAL  LWK_TRID_01
  DEFINE_LOCAL  LWK_TRID_02

  _EYE_TRAINER_ID_GET( SCR_EYE_TR_0, LWK_TRID_01 )
  _EYE_TRAINER_ID_GET( SCR_EYE_TR_1, LWK_TRID_02 )

  //視線曲セット
  _TRAINER_BGM_SET( LWK_TRID_01 )
  
  _EYE_TRAINER_MOVE_SET( SCR_EYE_TR_0 )        //トレーナー移動処理呼び出し
  _EYE_TRAINER_MOVE_SET( SCR_EYE_TR_1 )        //トレーナー移動処理呼び出し
  _EYE_TRAINER_MOVE_DOUBLE() //移動終了待ち

  //移動終了後、会話へ
  _TRAINER_MSG_SET( LWK_TRID_01, TRMSG_FIGHT_START_1 )
  _BALLOONWIN_CLOSE()
  
  //移動終了後、会話へ
  _TRAINER_MSG_SET( LWK_TRID_02, TRMSG_FIGHT_START_2 )
  _BALLOONWIN_CLOSE()
  
  //TRID_01 == TRID_02のとき、ダブルバトルになる
  _TRAINER_BTL_SET( LWK_TRID_01, LWK_TRID_02 )

  _TRAINER_LOSE_CHECK( SCWK_ANSWER )

  IF $SCWK_ANSWER == SCR_BATTLE_RESULT_WIN THEN
    _TRAINER_WIN()
  ELSE
    _TRAINER_LOSE()
  ENDIF

  //トレーナーフラグセット
  _TRAINER_FLAG_SET(  LWK_TRID_01 )
  _TRAINER_FLAG_SET(  LWK_TRID_02 )

  _RET
}

//--------------------------------------------------------------------
//タッグ
//--------------------------------------------------------------------
ev_trainer_type_tag:
{
  DEFINE_LOCAL  LWK_TRID_01
  DEFINE_LOCAL  LWK_TRID_02

  _EYE_TRAINER_ID_GET( SCR_EYE_TR_0, LWK_TRID_01 )
  _EYE_TRAINER_ID_GET( SCR_EYE_TR_1, LWK_TRID_02 )

  //視線曲セット
  _TRAINER_BGM_SET( LWK_TRID_01 )

  _EYE_TRAINER_MOVE_SET( SCR_EYE_TR_0 )        //トレーナー移動処理呼び出し
  _EYE_TRAINER_MOVE_SINGLE( SCR_EYE_TR_0 )     //移動終了待ち

  //移動終了後、会話へ
  _TRAINER_MSG_SET( LWK_TRID_01, TRMSG_FIGHT_START_1 )
  _BALLOONWIN_CLOSE()

  //視線曲セット
  _TRAINER_BGM_SET( LWK_TRID_02 )

  _EYE_TRAINER_MOVE_SET( SCR_EYE_TR_1 )        //トレーナー移動処理呼び出し
  _EYE_TRAINER_MOVE_SINGLE( SCR_EYE_TR_1 )     //移動終了待ち

  //移動終了後、会話へ
  _TRAINER_MSG_SET( LWK_TRID_02, TRMSG_FIGHT_START_2 )
  _BALLOONWIN_CLOSE()

  _TRAINER_BTL_SET( LWK_TRID_01, LWK_TRID_02 )
  _TRAINER_LOSE_CHECK( SCWK_ANSWER )
  IF $SCWK_ANSWER == SCR_BATTLE_RESULT_WIN THEN
    _TRAINER_WIN()
  ELSE
    _TRAINER_LOSE()
  ENDIF

  //トレーナーフラグセット
  _TRAINER_FLAG_SET( LWK_TRID_01 )
  _TRAINER_FLAG_SET( LWK_TRID_02 )

  _RET
}

