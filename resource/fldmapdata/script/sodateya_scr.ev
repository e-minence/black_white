
//======================================================================
//                sodateya_scr.ev
//
//    スクリプトファイル：sodateya_scr用
//
//======================================================================

  .text

  .include  "scr_seq_def.h"
  .include  "../../message/dst/script/msg_sodateya_scr.h"
  .include  "r03r0201.h"
  //.include  "sodateya_scr.h"

//----------------------------------------------------------------------
//               スクリプト本体
//----------------------------------------------------------------------

//----------------------------------------------------------------------
//               スクリプトテーブル
//----------------------------------------------------------------------
_EVENT_DATA   ev_sodateya_grandpa
_EVENT_DATA   ev_sodateya_grandma
_EVENT_DATA_END


//----------------------------------------------------------------------
// ローカルワーク
//----------------------------------------------------------------------
DEFINE_LOCAL message_id            // メッセージID
DEFINE_LOCAL ask_retval            // 「はい」「いいえ」選択結果
DEFINE_LOCAL retval_adopt_core     // 「あずける」コア処理サブルーチンの結果
DEFINE_LOCAL retval_take_back_core // 「ひきとる」コア処理サブルーチンの結果

//----------------------------------------------------------------------
// 育て屋さんの状態
//----------------------------------------------------------------------
DEFINE_LOCAL scenario_flag     // シナリオフラグ
DEFINE_LOCAL first_talk        // 初回会話フラグ
DEFINE_LOCAL sodateya_have_egg // タマゴの有無
DEFINE_LOCAL sodateya_poke_num // 預けているポケモンの数
DEFINE_LOCAL sodateya_poke_max // 預けポケモン最大数

//----------------------------------------------------------------------
// プレイヤーの状態
//----------------------------------------------------------------------
DEFINE_LOCAL party_total_num   // 手持ちポケモン数
DEFINE_LOCAL party_battle_num  // 戦闘可能なポケモン数


//======================================================================
// サブルーチン：育て屋さんの状態を取得する
//======================================================================
get_sodateya_state:
{
  _CHECK_SODATEYA_HAVE_EGG( sodateya_have_egg )   // タマゴの有無
  _GET_SODATEYA_POKEMON_NUM( sodateya_poke_num )  // 預けポケの数

  // 初回会話フラグ
  _FLAG_CHECK( FE_SODATEYA_FIRSTTALK, first_talk )

  // 預けポケ最大数
  _FLAG_CHECK( FE_SODATEYAOLDMAN_OPEN, scenario_flag )
  IF $scenario_flag == TRUE THEN
    $sodateya_poke_max = 2
  ELSE
    $sodateya_poke_max = 1
  ENDIF
}
_RET


//======================================================================
// サブルーチン：プレイヤーの状態を取得する
//======================================================================
get_party_state:
{
  // 手持ちポケモン数
  _GET_PARTY_POKE_COUNT( party_total_num, POKECOUNT_MODE_TOTAL )  

  // 戦闘可能なポケモン数
  _GET_PARTY_POKE_COUNT( party_battle_num, POKECOUNT_MODE_BATTLE_ENABLE )
}
_RET



/**********************************************************************/
/*                                                                    */
/* ■育て屋じいさん                                                   */
/*                                                                    */
/**********************************************************************/
EVENT_START ev_sodateya_grandpa
{ 
  // ローカルワーク宣言
  DEFINE_LOCAL egg_get      // タマゴを受け取るかどうか
  $egg_get = FALSE

  // 現在状態を取得
  _CALL get_sodateya_state    // 育て屋の状態
  _CALL get_party_state       // 手持ちの状態

  // 話しかけ開始
  _TALK_OBJ_START()

  //-------------------
  // タマゴがある場合
  //-------------------
  IF $sodateya_have_egg == TRUE THEN  

    //「タマゴほしいじゃろ？」
    _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldman_07, SCWK_TARGET_OBJID, POS_DOWN )
    _YES_NO_WIN( ask_retval )
    IF $ask_retval == 0 THEN  //「はい」を選択 ==> タマゴを貰う
      $egg_get = TRUE 
    ELSE                      //「いいえ」を選択 ==>「本当は欲しいんじゃろ？」
      _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldman_10, SCWK_TARGET_OBJID, POS_DOWN )
      _YES_NO_WIN( ask_retval )
      IF $ask_retval == 0 THEN  //「はい」を選択 ==> タマゴを貰う
        $egg_get = TRUE 
      ENDIF
    ENDIF

    // タマゴ受け取り処理
    IF $egg_get == TRUE THEN 
      IF $party_total_num < 6 THEN  // 手持ちに空き有 ==> たまごゲット
        _GET_SODATEYA_EGG()
        //「タマゴを貰った」
        _BALLOONWIN_CLOSE()
        _PLAYER_NAME( 0 )
        _ME_PLAY( SEQ_ME_TAMAGO_GET )
        _SYSWIN_MSG( msg_sodateya_oldman_08, POS_DEFAULT )
        _ME_WAIT()
        _AB_KEYWAIT_MARK()
        _SYSWIN_CLOSE()
        //「大事に育てなさいよ」
        _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldman_08_01, SCWK_TARGET_OBJID, POS_DOWN )
        _LAST_KEYWAIT()
        _BALLOONWIN_CLOSE()
      ELSE                      // 手持ちがいっぱい ==>「手持ちがいっぱいじゃ」
        _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldman_09, SCWK_TARGET_OBJID, POS_DOWN )
        _LAST_KEYWAIT()
        _BALLOONWIN_CLOSE()
      ENDIF
    // タマゴ削除
    ELSE
      _DELETE_SODATEYA_EGG()
			//「それじゃ わしが貰おう」
			_BALLOONWIN_OBJMSG_POS( msg_sodateya_oldman_11, SCWK_TARGET_OBJID, POS_DOWN )
			_LAST_KEYWAIT()
      _BALLOONWIN_CLOSE()
    ENDIF
  //-------------------
  // タマゴがない場合
  //-------------------
  ELSE                      
    IF $sodateya_poke_num == 0 THEN       // 預けポケモン0体 ==>「わしは育て屋じいさん」
      _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldman_01, SCWK_TARGET_OBJID, POS_DOWN )
      _LAST_KEYWAIT()
      _BALLOONWIN_CLOSE()
    ELSIF $sodateya_poke_num == 1 THEN    // 預けポケモン1体 ==>「よくきたな」
      _SODATEYA_NICK_NAME( 0, 0 )
      _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldman_02, SCWK_TARGET_OBJID, POS_DOWN )
      _LAST_KEYWAIT()
      _BALLOONWIN_CLOSE()
    ELSE                            // 預けポケモン2体 ==> 相性チェック
      _CALL love_check
    ENDIF
  ENDIF
} 
EVENT_END


//======================================================================
// サブルーチン：相性チェックとその結果発表
//======================================================================
love_check:
{
  DEFINE_LOCAL love

  // 相性チェック
  _SODATEYA_LOVE_CHECK( love )

  // 相性に応じて表示するメッセージを決定
  IF $love >= 70 THEN 
    $message_id = msg_sodateya_oldman_03
  ELSIF $love >= 50 THEN
    $message_id = msg_sodateya_oldman_04
  ELSIF $love >= 20 THEN
    $message_id = msg_sodateya_oldman_05
  ELSE
    $message_id = msg_sodateya_oldman_06
  ENDIF

  // 結果発表
  _SODATEYA_NICK_NAME( 0, 0 )
  _SODATEYA_NICK_NAME( 1, 1 )
  _BALLOONWIN_OBJMSG_POS( message_id, SCWK_TARGET_OBJID, POS_DOWN )
  _LAST_KEYWAIT()
  _BALLOONWIN_CLOSE()
}
_RET


/**********************************************************************/
/*                                                                    */
/* ■育て屋ばあさん                                                   */
/*                                                                    */
/**********************************************************************/
EVENT_START ev_sodateya_grandma
{
  // ローカルワーク宣言
  DEFINE_LOCAL menu_select    // メニュー選択結果

  // 現在の状態を取得
  _CALL get_sodateya_state
  _CALL get_party_state

  // 話しかけ開始
  _TALK_OBJ_START()

  //------------------
  // タマゴがある場合
  //------------------
  IF $sodateya_have_egg == TRUE THEN
    //「そういえば」
    _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_04, SCWK_TARGET_OBJID, POS_DOWN )
    _LAST_KEYWAIT()
    _BALLOONWIN_CLOSE() 
  //------------------
  // タマゴがない場合
  //------------------
  ELSE 
    _GOLD_WIN_OPEN( MENU_X1, MENU_Y1 ) // 所持金表示

    // 初回会話
    IF $first_talk == FALSE THEN
      //「あたし育て屋ばあさん」
      _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_01, SCWK_TARGET_OBJID, POS_DOWN )
      _FLAG_SET( FE_SODATEYA_FIRSTTALK ) // 初回会話が完了
    // 2回目以降
    ELSE
      //「何をしにきたの？」
      _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_02, SCWK_TARGET_OBJID, POS_DOWN )
    ENDIF

    // メニュー作成
    _BMPMENU_INIT_RIGHT( MENU_X2, MENU_Y2, 0, 1, menu_select )
    IF $sodateya_poke_num < $sodateya_poke_max THEN   // 『あずける』
      _BMPMENU_MAKE_LIST( msg_sodateya_menu_02, msg_sodateya_menu_02 )
    ENDIF
    IF $sodateya_poke_num > 0 THEN    // 『ひきとる』
      _BMPMENU_MAKE_LIST( msg_sodateya_menu_03, msg_sodateya_menu_03 )
    ENDIF
    _BMPMENU_MAKE_LIST( msg_sodateya_menu_04, msg_sodateya_menu_04 )  // 『やめる』

    // メニュー表示
    _BMPMENU_START()

    // 選択結果で分岐
    SWITCH $menu_select
    CASE msg_sodateya_menu_02 THEN //『あずける』
      _CALL sodateya_adopt_pokemon
    CASE msg_sodateya_menu_03 THEN //『ひきとる』
      _CALL player_take_back_pokemon
    CASE msg_sodateya_menu_04, EV_WIN_B_CANCEL THEN //『やめる』or『Bキャンセル』
      //「そうかい またおいで」
      _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_03, SCWK_TARGET_OBJID, POS_DOWN )
      _LAST_KEYWAIT()
      _BALLOONWIN_CLOSE() 
    ENDSWITCH

    _GOLD_WIN_CLOSE() // 所持金非表示

  ENDIF
}
EVENT_END 

//======================================================================
// サブルーチン：『あずける』コア部
//
// キャンセルした場合   : retval_adopt_core = 0
// タマゴを選択した場合 : retval_adopt_core = 1
// ポケモンを預けた場合 : retval_adopt_core = 2
//======================================================================
sodateya_adopt_pokemon_core:
{
  DEFINE_LOCAL select_party_pos   // 選択ポケモン
  DEFINE_LOCAL select_poke_is_egg // 選択ポケモンがタマゴかどうか
  DEFINE_LOCAL monsno
  DEFINE_LOCAL formno

  // ポケモン選択
  _SODATEYA_SELECT_POKEMON( select_party_pos )
  _GOLD_WIN_OPEN( MENU_X1, MENU_Y1 ) // 所持金表示

  //------------------------
  // ポケモンを選択した場合
  //------------------------
  IF $select_party_pos < $party_total_num THEN
    // 選択ポケモンがタマゴかどうかを調べる
    _CHECK_TEMOTI_EGG( select_poke_is_egg, select_party_pos )
    // 選択ポケモンがタマゴの場合
    // ※育て屋専用のポケモンリスト画面では、
    //   タマゴを選択できないため本来は必要ない処理だが、念のためチェックする。
    IF $select_poke_is_egg == TRUE THEN
      $retval_adopt_core = 1
    // 選択ポケモンがタマゴでない場合
    ELSE
      // 鳴き声SE
      _GET_PARTY_POKE_MONSNO( monsno, select_party_pos )
      _GET_PARTY_POKE_FORMNO( formno, select_party_pos )
      _VOICE_PLAY_FOR_MINE( monsno, formno )

      //「[ニックネーム]を しばらく預かるぞ」
      _TEMOTI_NICK_NAME( 0, select_party_pos )
      _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_08, SCWK_TARGET_OBJID, POS_DOWN )

      // 育て屋へデータ移動
      _POKE_TO_SODATEYA( select_party_pos )

      // レコード加算
      _ADD_RECORD( RECID_SODATEYA_CNT, 1 )

      $retval_adopt_core = 2
    ENDIF
  //------------------------
  // キャンセルした場合
  //------------------------
  ELSE
    $retval_adopt_core = 0
  ENDIF

  // 現在の状態を更新
  _CALL get_sodateya_state
  _CALL get_party_state
}
_RET

//======================================================================
// サブルーチン：『あずける』処理
//======================================================================
sodateya_adopt_pokemon: 
{
  DEFINE_LOCAL box_count
  DEFINE_LOCAL loop
  $loop = TRUE

  // あずけるループ
  WHILE $loop ==TRUE

    // 手持ちが1匹の場合 ==> 終了
    IF $party_battle_num == 1 THEN
      //「手持ちが1匹しかいないようじゃ」
      _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_05, SCWK_TARGET_OBJID, POS_DOWN )
      _LAST_KEYWAIT()
      _BALLOONWIN_CLOSE() 
      _RET
    ENDIF

    // 手持ちが2匹以下でボックスが空の場合 ==> 終了 ( はまり回避のため )
    IF $party_battle_num <= 2 THEN
      _GET_BOX_POKE_COUNT( box_count, POKECOUNT_MODE_NOT_EGG )
      IF $box_count == 0 THEN 
        _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_17, SCWK_TARGET_OBJID, POS_DOWN )
        _LAST_KEYWAIT()
        _BALLOONWIN_CLOSE() 
        _RET
      ENDIF
    ENDIF

    //「どのこを 育てる？」
    _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_06, SCWK_TARGET_OBJID, POS_DOWN )
    //_AB_KEYWAIT()
    _BALLOONWIN_CLOSE() 

    // あずけコア処理
    _CALL sodateya_adopt_pokemon_core

    // ポケモンを預けた場合
		SWITCH $retval_adopt_core
		CASE 0 THEN	// キャンセル ==> 終了
			//「そうかい またおいで」
      _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_03, SCWK_TARGET_OBJID, POS_DOWN )
      _LAST_KEYWAIT()
      _BALLOONWIN_CLOSE() 
      _RET
		CASE 1 THEN // タマゴを選択 ==> 終了
      //「それは育てられんぞ」
      _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_07, SCWK_TARGET_OBJID, POS_DOWN )
      _LAST_KEYWAIT()
      _BALLOONWIN_CLOSE() 
      _RET
    CASE 2 THEN // 預け完了
      // 最大数まで預けていなければ, ループする
      IF $sodateya_poke_num < $sodateya_poke_max THEN
        //「もう1匹 預けるかい？」
        _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_10, SCWK_TARGET_OBJID, POS_DOWN )
        _YES_NO_WIN( ask_retval )
        IF $ask_retval == 1 THEN   //『いいえ』を選択 ==> 終了
          $loop = FALSE 
        ENDIF
      // 最大数まであずけたらループを抜ける
      ELSE
        $loop = FALSE
      ENDIF
		ENDSWITCH

  ENDWHILE

  //「時間がたったら また来なさい」
  _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_09, SCWK_TARGET_OBJID, POS_DOWN )
  _LAST_KEYWAIT()
  _BALLOONWIN_CLOSE() 
}
_RET

//======================================================================
// サブルーチン：『ひきとる』コア部
//
// 手持ちがいっぱいの場合    : retval_take_back_core = 3
// ポケモンを引き取った場合  : retval_take_back_core = 2
// ポケモンを引き取らない場合: retval_take_back_core = 1
// お金が足りない場合        : retval_take_back_core = 0
//======================================================================
take_back_pokemon_core:
{
  // ローカルワークの定義
  DEFINE_LOCAL select_sodateya_pos // 引き取るポケモンの育て屋内インデックス
  DEFINE_LOCAL now_level           // 現在のレベル
  DEFINE_LOCAL grow_level          // 育ったレベル
  DEFINE_LOCAL charge              // 引き取り料金
  DEFINE_LOCAL have_gold           // 所持金が足りるかどうか
  DEFINE_LOCAL monsno              // 種族番号
  DEFINE_LOCAL formno              // 形状番号
  DEFINE_LOCAL sex                 // 性別
  DEFINE_LOCAL index

  //--------------
  // メニュー表示
  //--------------
  _BMPMENU_INIT_RIGHT( MENU_X2, MENU_Y2, 0, 1, select_sodateya_pos )

  $index = 0
  WHILE $index < $sodateya_poke_num

    // 基本情報を取得
    _GET_SODATEYA_POKE_MONSNO( monsno, index )
    _GET_SODATEYA_POKE_SEX( sex, index )
    _GET_SODATEYA_POKE_LV( now_level, index )
    // ワードセット登録
    _SODATEYA_NICK_NAME( 0, index ) // ニックネーム
    _NUMBER( 1, now_level, 3 ) // レベル
    // メニュー分類を取得
    _SODATEYA_CHECK( SCWK_ANSWER, SCR_SODATEYA_CHECK_ID_TAKEBACK_MENU, index )
    // メニュー項目を追加
    SWITCH $SCWK_ANSWER
    CASE SCR_SODATEYA_CHECK_RESULT_TAKEBACK_MENU_UNKNOWN THEN _BMPMENU_MAKE_LIST( msg_sodateya_menu_08, index ) // 性別なし
    CASE SCR_SODATEYA_CHECK_RESULT_TAKEBACK_MENU_MALE    THEN _BMPMENU_MAKE_LIST( msg_sodateya_menu_06, index ) // オス
    CASE SCR_SODATEYA_CHECK_RESULT_TAKEBACK_MENU_FEMALE  THEN _BMPMENU_MAKE_LIST( msg_sodateya_menu_07, index ) // メス
    ENDSWITCH

    _ASM_ADD_WK index, 1

  ENDWHILE

  _BMPMENU_MAKE_LIST( msg_sodateya_menu_04, 2 ) //『やめる』
  _BMPMENU_START()

  //-----------------------------------
  // 引き取らない場合(『やめる』を選択)
  //-----------------------------------
  IF ($select_sodateya_pos == 2) || ($select_sodateya_pos == EV_WIN_B_CANCEL) THEN
    $retval_take_back_core = 1
    _RET
  ENDIF 

  //------------------------------------
  // 引き取る場合(『ポケモン名』を選択)
  //------------------------------------
  // 選択ポケモンのLvUp判定
  _GET_SODATEYA_POKE_GROWUP_LV( grow_level, select_sodateya_pos )
  _GET_SODATEYA_POKE_CHARGE( charge, select_sodateya_pos )

  // Lv上がる場合
  IF $grow_level > 0 THEN
    //「○○くらい育っておるぞ」
    _SODATEYA_NICK_NAME( 0, select_sodateya_pos )
    _NUMBER( 1, grow_level, 2 )
    _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_12, SCWK_TARGET_OBJID, POS_DOWN )
  ENDIF

  // 質問「引き取るかい?」
  _SODATEYA_NICK_NAME( 0, select_sodateya_pos )
  _NUMBER( 1, charge, 5 )
  _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_13, SCWK_TARGET_OBJID, POS_DOWN ) 
  _YES_NO_WIN( ask_retval )

  //『いいえ』を選択
  IF $ask_retval == 1 THEN
    $retval_take_back_core = 1
    _RET
  ENDIF

  // 『はい』を選択
  _CHECK_GOLD( have_gold, charge ) // 所持金チェック

  // 所持金が足りない場合 ==> 受け取れず終了
  IF $have_gold == FALSE THEN
    $retval_take_back_core = 0
    _RET
  ENDIF

  // 所持金が足りる場合
  _GET_PARTY_POKE_COUNT( SCWK_ANSWER, POKECOUNT_MODE_EMPTY ) // 手持ちチェック

  // 手持ちがいっぱいの場合 ==> 受け取れず終了
  IF $SCWK_ANSWER == 0 THEN
    $retval_take_back_core = 3
    _RET
  ENDIF

  // 手持ちに空きがある場合

  // お金を払う
  _SUBTRACT_GOLD( charge )  // 所持金を減らす
  _GOLD_WIN_UPDATE()        // 所持金ウィンドウを更新
  _SE_PLAY( SEQ_SE_SYS_22 ) // 支払いSE
  _SE_WAIT()                //
  _BALLOONWIN_CLOSE()       // 吹出しウィンドウ消去

  //引き取りアニメ演出
  _OBJ_ANIME( R03R0201_SODATEYA_GRANDMA, anime_grandma_take_back )
  _OBJ_ANIME_WAIT()

  // 鳴き声SE
  _GET_SODATEYA_POKE_MONSNO( monsno, select_sodateya_pos )
  _GET_SODATEYA_POKE_FORMNO( formno, select_sodateya_pos )
  _VOICE_PLAY_FOR_MINE( monsno, formno )

  //「[ニックネーム]を引き取った！」
  _SODATEYA_NICK_NAME( 0, select_sodateya_pos )
  _PLAYER_NAME( 1 )
  _SYSWIN_MSG( msg_sodateya_oldwoman_15, POS_DOWN )
  _SYSWIN_CLOSE()

  // 手持ちポケモンに移動
  _POKE_FROM_SODATEYA( select_sodateya_pos ) 
  $retval_take_back_core = 2
}
_RET


//======================================================================
// 動作モデル アニメーション: おばあさん 引き取り時の動作
//======================================================================
_ANIME_LABEL anime_grandma_take_back
  _ANIME_DATA AC_WALK_U_16F, 3
  _ANIME_DATA AC_VANISH_ON,  1
  _ANIME_DATA AC_WAIT_16F,   1
  _ANIME_DATA AC_DIR_D,      1
  _ANIME_DATA AC_VANISH_OFF, 1
  _ANIME_DATA AC_WALK_D_16F, 3
  _ANIME_DATA ACMD_END,      0


//======================================================================
// サブルーチン：『ひきとる』処理
//======================================================================
player_take_back_pokemon:
{
  // ローカルワークの定義
  DEFINE_LOCAL loop       // FALSEになると繰り返し終了

  //「ポケモンをひきとるかい？」
  _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_11, SCWK_TARGET_OBJID, POS_DOWN )
  //_AB_KEYWAIT()

  // 引き取りループ
  $loop = TRUE
  WHILE $loop == TRUE
    // コア処理
    _CALL take_back_pokemon_core 

    // 現在の状態を更新
    _CALL get_sodateya_state
    _CALL get_party_state
    
    // コア処理の結果で分岐
    SWITCH $retval_take_back_core
    CASE 0 THEN // お金が足りなかった場合 ==> 終了
      //「お金が足りないよ」
      _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_14, SCWK_TARGET_OBJID, POS_DOWN )
      _LAST_KEYWAIT()
      $loop = FALSE
    CASE 3 THEN // 手持ちがいっぱいな場合 ==> 終了
      //「手持ちがいっぱいだよ」
      _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldman_09, SCWK_TARGET_OBJID, POS_DOWN )
      _LAST_KEYWAIT()
      $loop = FALSE
    CASE 1 THEN // 引き取らなかった場合 ==> 終了
      //「そうかい またおいで」
      _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_03, SCWK_TARGET_OBJID, POS_DOWN )
      _LAST_KEYWAIT()
      $loop = FALSE
    CASE 2 THEN // 引き取った場合
      // 全ポケモンを引き取った場合 ==> 終了
      IF $sodateya_poke_num == 0 THEN
        //「それじゃあ またきておくれ」
        _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_03, SCWK_TARGET_OBJID, POS_DOWN )
        _LAST_KEYWAIT()
        $loop = FALSE
      // まだ預けポケモンがいる場合
      ELSE
        // 質問「もう一匹 引き取るかい?」
        _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_16, SCWK_TARGET_OBJID, POS_DOWN )
        _YES_NO_WIN( ask_retval )
        //『いいえ』==> 終了
        IF $ask_retval == 1 THEN
          //「そうかい またおいで」
          _BALLOONWIN_OBJMSG_POS( msg_sodateya_oldwoman_03, SCWK_TARGET_OBJID, POS_DOWN )
          _LAST_KEYWAIT()
          $loop = FALSE
        ENDIF
      ENDIF
    ENDSWITCH

  ENDWHILE

  _BALLOONWIN_CLOSE() 
}
_RET 
