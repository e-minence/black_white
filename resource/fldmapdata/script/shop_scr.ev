//======================================================================
//                shop_scr.ev
//    ショップ関連スクリプト
//
//    スクリプトファイル：shop_scr用
//
//======================================================================

  .text

  .include  "scr_seq_def.h"
  .include  "msg_shop_scr.h"
  .include  "shop_scr_local.h"
  //.include  "wifi_club_scr.h"

//  .include  "../../../prog/include/system/timezone.h" //時間帯定義

//--------------------------------------------------------------
//               スクリプト本体
//--------------------------------------------------------------
//--------------------------------------------------------------
//               スクリプトテーブル
//--------------------------------------------------------------
_EVENT_DATA    ev_shop_counter
_EVENT_DATA_END

DEFINE_LOCAL  reg_return

{ //SHOP_MAIN

DEFINE_LOCAL  lwk_shop_id

//==============================================================
/// 
///
///       ポケモンセンター　ショップ受付                   
///
///
//==============================================================
ev_shop_counter:
{
  $lwk_shop_id = $SCWK_PARAM0
  _CALL sub_shop_main
}
_CHG_LOCAL_SCR

//==============================================================
//サブルーチン：フレンドリィショップメイン
//==============================================================
sub_shop_main:
{
  DEFINE_LOCAL  i_ret
  DEFINE_LOCAL  i_seq
  DEFINE_LOCAL  i_loop

  //ようこそ
  _TALKMSG( msg_shop_02 )
  _TALKMSG( msg_shop_01 )

  $i_seq = SEQ_MENU
  $i_loop = TRUE
  WHILE $i_loop == TRUE
    SWITCH $i_seq
    CASE SEQ_MENU_CONTINUE THEN
      _TALKMSG( msg_shop_02 ) //そのほかにお手伝いできることは？
      $i_seq = SEQ_MENU

    CASE SEQ_MENU THEN
      _CALL sub_shop_menu
      $i_seq = $reg_return
    
    CASE SEQ_BUY THEN
      _CALL sub_shop_buy
      $i_seq = $reg_return 
    
    CASE SEQ_SELL THEN
      _CALL sub_shop_sell
      $i_seq = $reg_return 

    CASE SEQ_EXIT THEN
      $i_loop = FALSE
    ENDSWITCH
  ENDWHILE
  
  _TALKMSG( msg_shop_03 ) //またどうぞ
  _LAST_KEYWAIT()
  _TALKWIN_CLOSE()
}
_RET

//==============================================================
//サブルーチン：メニュー表示
//==============================================================
sub_shop_menu:
{
  DEFINE_LOCAL  i_ret

  //分岐・Wi-Fiクラブへいく・せつめいをきく・やめる
	_BMPMENU_INIT_EX( 1, 1, 0, 1, i_ret )
		
	_BMPMENU_MAKE_LIST( msg_shop_menu01_01, SEQ_BUY )	//かう
	_BMPMENU_MAKE_LIST( msg_shop_menu01_02, SEQ_SELL )	//うる
	_BMPMENU_MAKE_LIST( msg_shop_menu01_03, SEQ_EXIT )	//やめる
		
	//メニュー表示
	_BMPMENU_START()
  
  SWITCH $i_ret
  CASE SEQ_BUY THEN
    $reg_return = SEQ_BUY
    _RET 
  CASE SEQ_SELL THEN
    $reg_return = SEQ_SELL
    _RET
  ENDSWITCH
  $reg_return = SEQ_EXIT 
}
_RET

//==============================================================
//サブルーチン：ショップ購入呼び出し
//==============================================================
sub_shop_buy:
{
  DEFINE_LOCAL  ret_mode

  $ret_mode = SCR_PROC_RETMODE_NORMAL

//  _CAMERA_MOVE(0,0,0, (16*5),0,0, 30)
  _CALL_SHOP_PROC_BUY( lwk_shop_id, ret_mode )
//  _CAMERA_RECV_MOVE(30)
  
  IF $ret_mode == SCR_PROC_RETMODE_EXIT THEN
    $reg_return = SEQ_EXIT
  ELSE
    $reg_return = SEQ_MENU_CONTINUE
  ENDIF
}
_RET

//==============================================================
//サブルーチン：ショップ売却呼び出し
//==============================================================
sub_shop_sell:
{
  DEFINE_LOCAL  ret_mode
  DEFINE_LOCAL  ret_item

  _BLACK_OUT( DISP_FADE_SPEED )
  _DISP_FADE_END_CHECK()
  _TALKWIN_CLOSE()
  _FIELD_CLOSE()
  
  _CALL_BAG_PROC( SCR_BAG_MODE_SELL )
  _GET_BAG_PROC_RESULT( ret_mode, ret_item )
  _FREE_SUBPROC_WORK()

  _FIELD_OPEN()
  _BLACK_IN( DISP_FADE_SPEED )
  _DISP_FADE_END_CHECK()

  IF $ret_mode == SCR_PROC_RETMODE_EXIT THEN
    $reg_return = SEQ_EXIT
  ELSE
    $reg_return = SEQ_MENU_CONTINUE
  ENDIF
}
_RET

} //SHOP_MAIN_EN
