//======================================================================
//                shop_scr.ev
//    ショップ関連スクリプト
//
//    スクリプトファイル：shop_scr用
//
//======================================================================

  .text

  .include  "scr_seq_def.h"
  .include  "msg_shop_scr.h"
  .include  "shop_scr_local.h"
  //.include  "wifi_club_scr.h"

//  .include  "../../../prog/include/system/timezone.h" //時間帯定義

//--------------------------------------------------------------
//               スクリプト本体
//--------------------------------------------------------------
//--------------------------------------------------------------
//               スクリプトテーブル
//--------------------------------------------------------------
_EVENT_DATA    ev_shop_counter
_EVENT_DATA_END

DEFINE_LOCAL  reg_return

{ //SHOP_MAIN

DEFINE_LOCAL  lwk_shop_id
DEFINE_LOCAL  lwk_greeting

//==============================================================
/// 
///
///       ポケモンセンター　ショップ受付                   
///
///
//==============================================================
ev_shop_counter:
{
  $lwk_shop_id = $SCWK_PARAM0
  
  IF $SCWK_PARAM1 == TRUE THEN
    //挨拶「いらっしゃいませ」
    _BALLOONWIN_TALKOBJ_MSG( msg_shop_greeting )
    _AB_KEYWAIT()
    _BALLOONWIN_CLOSE()
  ENDIF

  _CALL sub_shop_main
}
_CHG_LOCAL_SCR

//==============================================================
//サブルーチン：フレンドリィショップメイン
//==============================================================
sub_shop_main:
{
  DEFINE_LOCAL  i_ret
  DEFINE_LOCAL  i_seq
  DEFINE_LOCAL  i_loop

  //ようこそ
  _SYSWIN_MSG_DOWN( msg_shop_01 )

  $i_seq = SEQ_MENU
  $i_loop = TRUE
  WHILE $i_loop == TRUE
    SWITCH $i_seq
    CASE SEQ_MENU_CONTINUE THEN
      _SYSWIN_MSG_DOWN( msg_shop_02 ) //そのほかにお手伝いできることは？
      $i_seq = SEQ_MENU

    CASE SEQ_MENU THEN
      _CALL sub_shop_menu
      $i_seq = $reg_return
    
    CASE SEQ_BUY THEN
      _CALL sub_shop_buy
      $i_seq = $reg_return 
    
    CASE SEQ_SELL THEN
      _CALL sub_shop_sell
      $i_seq = $reg_return 

    CASE SEQ_EXIT THEN
      $i_loop = FALSE
    ENDSWITCH
  ENDWHILE
  
  _SYSWIN_MSG_DOWN( msg_shop_03 ) //またどうぞ
  _LAST_KEYWAIT()
  _SYSWIN_CLOSE()
}
_RET

//==============================================================
//サブルーチン：メニュー表示
//==============================================================
sub_shop_menu:
{
  DEFINE_LOCAL  i_ret

  //分岐・Wi-Fiクラブへいく・せつめいをきく・やめる
	_BMPMENU_INIT_EX( 1, 1, 0, 1, i_ret )
		
	_BMPMENU_MAKE_LIST( msg_shop_menu01_01, SEQ_BUY )	//かう
	_BMPMENU_MAKE_LIST( msg_shop_menu01_02, SEQ_SELL )	//うる
	_BMPMENU_MAKE_LIST( msg_shop_menu01_03, SEQ_EXIT )	//やめる
		
	//メニュー表示
	_BMPMENU_START()
  
  IF $i_ret == EV_WIN_B_CANCEL THEN
    $reg_return = SEQ_EXIT
  ELSE
    $reg_return = $i_ret
  ENDIF
}
_RET

//==============================================================
//サブルーチン：ショップ購入呼び出し
//==============================================================
sub_shop_buy:
{
  DEFINE_LOCAL  ret_mode
  
  _SYSWIN_CLOSE()

  $ret_mode = SCR_PROC_RETMODE_NORMAL
  _CALL_SHOP_PROC_BUY( lwk_shop_id, ret_mode )
  
  IF $ret_mode == SCR_PROC_RETMODE_EXIT THEN
    $reg_return = SEQ_EXIT
  ELSE
    $reg_return = SEQ_MENU_CONTINUE
  ENDIF
}
_RET

//==============================================================
//サブルーチン：ショップ売却呼び出し
//==============================================================
sub_shop_sell:
{
  DEFINE_LOCAL  ret_mode
  DEFINE_LOCAL  ret_item

  _BLACK_OUT()
  _DISP_FADE_END_CHECK()
  _SYSWIN_CLOSE()
  _FIELD_CLOSE()
  
  _CALL_BAG_PROC( SCR_BAG_MODE_SELL,ret_mode, ret_item )

  _FIELD_OPEN()
  _BLACK_IN()
  _DISP_FADE_END_CHECK()

  IF $ret_mode == SCR_PROC_RETMODE_EXIT THEN
    $reg_return = SEQ_EXIT
  ELSE
    $reg_return = SEQ_MENU_CONTINUE
  ENDIF
}
_RET

} //SHOP_MAIN_EN
