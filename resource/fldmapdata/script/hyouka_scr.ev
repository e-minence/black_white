//====================================================================
//            hyouka_scr.ev
//            ずかん評価スクリプト
//====================================================================

  .text
  .include  "scr_seq_def.h"
  .include  "msg_hyouka_scr.h"
  
//--------------------------------------------------------------------
//               スクリプト本体
//--------------------------------------------------------------------
_EVENT_DATA   ev_zukan_daughter
_EVENT_DATA   ev_zukan_father
_EVENT_DATA   ev_zukan_machine
_EVENT_DATA_END //終了

//--------------------------------------------------------------------
//--------------------------------------------------------------------

//====================================================================
//
//====================================================================
  DEFINE_LOCAL    HYOUKA_MSGID
  DEFINE_LOCAL    HYOUKA_COUNT
  DEFINE_LOCAL    ITEM_RESULT_COUNT
  DEFINE_LOCAL    ITEM_RESULT_01
  DEFINE_LOCAL    ITEM_RESULT_02
  DEFINE_LOCAL    ITEM_RESULT_03

/**************************************************************/
/*    図鑑評価：アララギ博士（娘）                            */
/**************************************************************/
ev_zukan_daughter:
{
  _PLAYER_NAME( 0 )
  _BALLOONWIN_TALKOBJ_MSG( msg_hyouka_hakase2_01 )
  _YES_NO_WIN( SCWK_ANSWER )

  IF $SCWK_ANSWER == SCR_YES THEN
  //ずかんを見せる→「はい」
      _GET_ZUKAN_HYOUKA( SCR_ZUKAN_HYOUKA_MODE_LOCAL_SEE, HYOUKA_MSGID, HYOUKA_COUNT )

      IF FLAG_ON( FE_ZUKAN_COMPLETE_LOCAL_SEE ) THEN
      //地方図鑑・見たをコンプリート→捕まえた数
          _CALL sub_zukan_setup_local_get
          _BALLOONWIN_TALKOBJ_MSG( msg_hyouka_hakase2_02a )
          _CALL sub_zuken_local_get_me_play
          _ME_WAIT()
          _AB_KEYWAIT_MARK()
          _BALLOONWIN_TALKOBJ_MSG( HYOUKA_MSGID )
      ELSE
      //地方図鑑・見た数
          _CALL sub_zukan_setup_local_see
          _BALLOONWIN_TALKOBJ_MSG( msg_hyouka_hakase2_02 )
          _CALL sub_zuken_local_see_me_play
          _ME_WAIT()
          _AB_KEYWAIT_MARK()
          _BALLOONWIN_TALKOBJ_MSG( HYOUKA_MSGID )
      ENDIF

      //アイテムをもらえるか？チェック
      _CALL sub_zukan_present_check
      IF $ITEM_RESULT_COUNT != 0 THEN
        //「なんだかいいかんじで〜プレゼントするわ！」
        _BALLOONWIN_TALKOBJ_MSG( msg_hyouka_hakase2_05 )
        _BALLOONWIN_CLOSE()
        //どうぐプレゼント処理をコール
        _CALL sub_zukan_present
      ENDIF

      //「これからもよろしくね！」
      _BALLOONWIN_TALKOBJ_MSG( msg_hyouka_hakase2_10 )
      _LAST_KEYWAIT()
      _BALLOONWIN_CLOSE()


  ELSE
  //ずかんを見せる→「いいえ」
      _BALLOONWIN_TALKOBJ_MSG( msg_hyouka_hakase2_03 )
      _LAST_KEYWAIT()
      _BALLOONWIN_CLOSE()
  ENDIF

  _CHG_LOCAL_SCR
}

//--------------------------------------------------------------------
//地方図鑑：見た数評価
//--------------------------------------------------------------------
sub_zukan_setup_local_see:
    //見た図鑑コンプリートだったらフラグをセット
    _GET_ZUKAN_COMPLETE( SCWK_ANSWER, SCR_ZUKAN_HYOUKA_MODE_LOCAL_SEE )
    IF $SCWK_ANSWER == TRUE THEN
        _FLAG_SET( FE_ZUKAN_COMPLETE_LOCAL_SEE )
    ENDIF

    _GET_ZUKAN_HYOUKA( SCR_ZUKAN_HYOUKA_MODE_LOCAL_SEE, HYOUKA_MSGID, HYOUKA_COUNT )

    _NUMBER( 0, HYOUKA_COUNT, 3 )
    _RET

//--------------------------------------------------------------------
//地方図鑑：捕まえた数評価
//--------------------------------------------------------------------
sub_zukan_setup_local_get:
    _GET_ZUKAN_HYOUKA( SCR_ZUKAN_HYOUKA_MODE_LOCAL_GET, HYOUKA_MSGID, HYOUKA_COUNT )

    _NUMBER( 0, HYOUKA_COUNT, 3 )
    _RET

//--------------------------------------------------------------------
//地方図鑑（見た）：ME再生
//  IN  HYOUKA_COUNT
//--------------------------------------------------------------------
sub_zuken_local_see_me_play:
    _GET_ZUKAN_COMPLETE( SCWK_ANSWER, SCR_ZUKAN_HYOUKA_MODE_LOCAL_SEE )
    IF $SCWK_ANSWER == TRUE THEN
        _ME_PLAY( SEQ_ME_HYOUKA6 )
    ELSE
        _CALL sub_zukan_local_count_me_play
    ENDIF
    _RET

//--------------------------------------------------------------------
//地方図鑑（捕まえた）：ME再生
//  IN  HYOUKA_COUNT
//--------------------------------------------------------------------
sub_zuken_local_get_me_play:
    _GET_ZUKAN_COMPLETE( SCWK_ANSWER, SCR_ZUKAN_HYOUKA_MODE_LOCAL_GET )
    IF $SCWK_ANSWER == TRUE THEN
        _ME_PLAY( SEQ_ME_HYOUKA6 )
    ELSE
        _CALL sub_zukan_local_count_me_play
    ENDIF
    _RET

//--------------------------------------------------------------------
//地方図鑑：ME再生
//  IN  HYOUKA_COUNT
//--------------------------------------------------------------------
sub_zukan_local_count_me_play:
    IF $HYOUKA_COUNT <= 24 THEN
        _ME_PLAY( SEQ_ME_HYOUKA1 )
    ELSIF $HYOUKA_COUNT <= 59 THEN
        _ME_PLAY( SEQ_ME_HYOUKA2 )
    ELSIF $HYOUKA_COUNT <= 114 THEN
        _ME_PLAY( SEQ_ME_HYOUKA3 )
    ELSIF $HYOUKA_COUNT <= 134 THEN
        _ME_PLAY( SEQ_ME_HYOUKA4 )
    ELSIF $HYOUKA_COUNT <= 149 THEN
        _ME_PLAY( SEQ_ME_HYOUKA5 )
    ELSE
        _ME_PLAY( SEQ_ME_HYOUKA5 )
    ENDIF
    _RET

//--------------------------------------------------------------------
//全国図鑑：ME再生
//  IN  HYOUKA_COUNT
//--------------------------------------------------------------------
sub_zukan_global_me_play:
    _GET_ZUKAN_COMPLETE( SCWK_ANSWER, SCR_ZUKAN_HYOUKA_MODE_GLOBAL )
    IF $SCWK_ANSWER == TRUE THEN
        //コンプリートのときだけ
        _ME_PLAY( SEQ_ME_HYOUKA6 )
    ELSIF $HYOUKA_COUNT <= 159 THEN
        _ME_PLAY( SEQ_ME_HYOUKA1 )
    ELSIF $HYOUKA_COUNT <= 349 THEN
        _ME_PLAY( SEQ_ME_HYOUKA2 )
    ELSIF $HYOUKA_COUNT <= 449 THEN
        _ME_PLAY( SEQ_ME_HYOUKA3 )
    ELSIF $HYOUKA_COUNT <= 549 THEN
        _ME_PLAY( SEQ_ME_HYOUKA4 )
    ELSIF $HYOUKA_COUNT <= 633 THEN
        _ME_PLAY( SEQ_ME_HYOUKA5 )
    ELSE
        _ME_PLAY( SEQ_ME_HYOUKA5 )
    ENDIF
    _RET

//--------------------------------------------------------------------
//どうぐもらえるか？チェック
//--------------------------------------------------------------------
sub_zukan_present_check:
  _GET_ZUKAN_HYOUKA( SCR_ZUKAN_HYOUKA_MODE_LOCAL_SEE, HYOUKA_MSGID, SCWK_ANSWER )

  $ITEM_RESULT_COUNT = 0
  IF FLAG_OFF( FE_HYOUKA_ITEMGET_01 ) && $SCWK_ANSWER >= 25 THEN
    $ITEM_RESULT_01 = 1
    $ITEM_RESULT_COUNT += 1
  ELSE
    $ITEM_RESULT_01 = 0
  ENDIF
  IF FLAG_OFF( FE_HYOUKA_ITEMGET_02 ) && $SCWK_ANSWER >= 60 THEN
    $ITEM_RESULT_02 = 1
    $ITEM_RESULT_COUNT += 1
  ELSE
    $ITEM_RESULT_02 = 0
  ENDIF
  IF FLAG_OFF( FE_HYOUKA_ITEMGET_03 ) && $SCWK_ANSWER >= 115 THEN
    $ITEM_RESULT_03 = 1
    $ITEM_RESULT_COUNT += 1
  ELSE
    $ITEM_RESULT_03 = 0
  ENDIF
  _RET

//--------------------------------------------------------------------
//  どうぐをプレゼントされる処理
//--------------------------------------------------------------------
sub_zukan_present:
{
  IF $ITEM_RESULT_01 != 0 THEN
    _ADD_ITEM_CHK( ITEM_WAZAMASIN54, 1, SCWK_ANSWER )
    IF $SCWK_ANSWER == FALSE THEN
      $SCWK_TEMP0 = ITEM_WAZAMASIN54
      _GLOBAL_CALL  SCRID_ITEM_EVENT_FAIL_KEYWAIT
      _RET
    ENDIF

    $SCWK_PARAM0  = ITEM_WAZAMASIN54  //「みねうち」
    $SCWK_PARAM1  = 1
    _GLOBAL_CALL  SCRID_ITEM_EVENT_KEYWAIT
    _BALLOONWIN_TALKOBJ_MSG( msg_hyouka_hakase2_06 )
    _FLAG_SET( FE_HYOUKA_ITEMGET_01 )

    $ITEM_RESULT_COUNT -= 1
    IF $ITEM_RESULT_COUNT != 0 THEN
      //「これもつかってみて！▼」
      _BALLOONWIN_TALKOBJ_MSG( msg_hyouka_hakase2_09 )
      _BALLOONWIN_CLOSE()
    ELSE
    ENDIF

  ENDIF

  IF $ITEM_RESULT_02 != 0 THEN
    _ADD_ITEM_CHK( ITEM_WAZAMASIN17, 1, SCWK_ANSWER )
    IF $SCWK_ANSWER == FALSE THEN
      $SCWK_TEMP0 = ITEM_WAZAMASIN17
      _GLOBAL_CALL  SCRID_ITEM_EVENT_FAIL_KEYWAIT
      _RET
    ENDIF

    $SCWK_PARAM0  = ITEM_WAZAMASIN17  //「まもる」
    $SCWK_PARAM1  = 1
    _GLOBAL_CALL  SCRID_ITEM_EVENT_KEYWAIT
    _BALLOONWIN_TALKOBJ_MSG( msg_hyouka_hakase2_07 )
    _FLAG_SET( FE_HYOUKA_ITEMGET_02 )

    $ITEM_RESULT_COUNT -= 1
    IF $ITEM_RESULT_COUNT != 0 THEN
      //「これもつかってみて！▼」
      _BALLOONWIN_TALKOBJ_MSG( msg_hyouka_hakase2_09 )
      _BALLOONWIN_CLOSE()
    ENDIF

  ENDIF

  IF $ITEM_RESULT_03 != 0 THEN
    _ADD_ITEM_CHK( ITEM_WAZAMASIN10, 1, SCWK_ANSWER )
    IF $SCWK_ANSWER == FALSE THEN
      $SCWK_TEMP0 = ITEM_WAZAMASIN10
      _GLOBAL_CALL  SCRID_ITEM_EVENT_FAIL_KEYWAIT
      _RET
    ENDIF

    $SCWK_PARAM0  = ITEM_WAZAMASIN10  //「めざめるパワー」
    $SCWK_PARAM1  = 1
    _GLOBAL_CALL  SCRID_ITEM_EVENT_KEYWAIT
    _BALLOONWIN_TALKOBJ_MSG( msg_hyouka_hakase2_08 )
    //_BALLOONWIN_CLOSE()
    _FLAG_SET( FE_HYOUKA_ITEMGET_03 )

  ENDIF
  _RET
}


/**************************************************************/
/*    図鑑評価：アララギ博士（父）                            */
/**************************************************************/
ev_zukan_father:
{
  _PLAYER_NAME( 0 )
  _BALLOONWIN_TALKOBJ_MSG( msg_hyouka_hakase_01 )
  _YES_NO_WIN( SCWK_ANSWER )

  IF $SCWK_ANSWER == SCR_YES THEN

      _GET_ZUKAN_HYOUKA( SCR_ZUKAN_HYOUKA_MODE_GLOBAL, HYOUKA_MSGID, HYOUKA_COUNT )

      _NUMBER( 0, HYOUKA_COUNT, 3 )
      _BALLOONWIN_TALKOBJ_MSG( msg_hyouka_hakase_02 )
      _CALL   sub_zukan_global_me_play
      _ME_WAIT()
      _AB_KEYWAIT_MARK()
      _BALLOONWIN_TALKOBJ_MSG( HYOUKA_MSGID )
      _LAST_KEYWAIT()
      _BALLOONWIN_CLOSE()

  ELSE
      _BALLOONWIN_TALKOBJ_MSG( msg_hyouka_hakase_03 )
      _LAST_KEYWAIT()
      _BALLOONWIN_CLOSE()
  ENDIF

  _CHG_LOCAL_SCR
}

/**************************************************************/
/*    図鑑評価：パソコンから                                  */
/**************************************************************/
ev_zukan_machine:
{
  _GET_ZENKOKU_ZUKAN_FLAG( SCWK_ANSWER )
  IF $SCWK_ANSWER == TRUE THEN
  //全国図鑑モード＝博士（父）の評価
      _GET_ZUKAN_HYOUKA( SCR_ZUKAN_HYOUKA_MODE_GLOBAL, HYOUKA_MSGID, HYOUKA_COUNT )

      _NUMBER( 0, HYOUKA_COUNT, 3 )
      _SYSWIN_MSG( msg_hyouka_hakase_02, POS_DOWN )
      _CALL   sub_zukan_global_me_play
      _ME_WAIT()
      _AB_KEYWAIT_MARK()
      _SYSWIN_MSG( HYOUKA_MSGID, POS_DOWN )
      _AB_KEYWAIT_MARK()
      //_SYSWIN_CLOSE() 呼び出し元（パソコン）で引き続きシステムMSG表示するので閉じない
  ELSE
  //地方図鑑モード＝博士（娘）の評価
      IF FLAG_ON( FE_ZUKAN_COMPLETE_LOCAL_SEE ) THEN
      //地方図鑑・見たをコンプリート→捕まえた数
          _CALL sub_zukan_setup_local_get
          _SYSWIN_MSG( msg_hyouka_hakase2_02a, POS_DOWN )
          _CALL sub_zuken_local_get_me_play
          _ME_WAIT()
          _AB_KEYWAIT_MARK()
          _SYSWIN_MSG( HYOUKA_MSGID, POS_DOWN )
      ELSE
      //地方図鑑・見た数
          _CALL sub_zukan_setup_local_see
          _SYSWIN_MSG( msg_hyouka_hakase2_02, POS_DOWN )
          _CALL sub_zuken_local_see_me_play
          _ME_WAIT()
          _AB_KEYWAIT_MARK()
          _SYSWIN_MSG( HYOUKA_MSGID, POS_DOWN )
      ENDIF
      //アイテムをもらえるか？チェック
      _CALL sub_zukan_present_check
      IF $ITEM_RESULT_COUNT != 0 THEN
        //「なんだかいいかんじで〜けんきゅうじょにきてね！」
          _SYSWIN_MSG( msg_hyouka_hakase2_04, POS_DOWN )
          _AB_KEYWAIT_MARK()
      ENDIF
    //_SYSWIN_CLOSE() 呼び出し元（パソコン）で引き続きシステムMSG表示するので閉じない
  ENDIF

  _CHG_LOCAL_SCR
}


