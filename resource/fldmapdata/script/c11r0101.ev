
//======================================================================
//                c11r0101.ev
//
//    スクリプトファイル：c11r0101用
//
//======================================================================

  .text

  .include  "scr_seq_def.h"
  .include  "msg_c11r0101.h"
  .include  "c11r0201_def.h"  //for reserve
  .include  "c11r0101.h"

//--------------------------------------------------------------
//               スクリプト本体
//--------------------------------------------------------------

//--------------------------------------------------------------
//               スクリプトテーブル
//--------------------------------------------------------------
_EVENT_DATA     ev_c11r0101_reception01
_EVENT_DATA     ev_c11r0101_reception02
_EVENT_DATA     ev_c11r0101_reception03
_EVENT_DATA     ev_c11r0101_backpackerm01
_EVENT_DATA     ev_c11r0101_boy01
_EVENT_DATA     ev_c11r0101_dancer01
_EVENT_DATA     ev_c11r0101_policeman01
_EVENT_DATA     ev_c11r0101_pos01
_EVENT_DATA     ev_reserve_c11r0101
_EVENT_DATA     ev_init_c11r0101
_EVENT_DATA_END

//------------------------------注意--------------------------------//
//	LOCALWORK1⇒POSイベント発動トリガー
//　　　POSイベント制御に使用します
//
//------------------------------------------------------------------//
//ローカル定義
DEFINE_LOCAL	DOOR_ANIME_ID

/**************************************************************/
/*            FIELD_INIT_LABEL                               */
/**************************************************************/
INIT_EVENT_START ev_init_c11r0101
  //POSイベント起動初期化
  $LOCALWORK1 = 0
  //エレベータから出てきたか？（国連部屋訪問状態か）
  IF $WK_OTHER_UN == 1 THEN   //エレベータから出てくる(国連部屋訪問状態ON)
    //受付の人を移動
    _OBJ_POS_CHANGE( UN_ENTRANCE_RECEPTION_01, 13, 0, 10, DIR_RIGHT )
  ELSE      //入り口から入ってくる or メニュー復帰
    //条件判定してPOSイベントをON
    _CALL check_posevt_start
  ENDIF
  //国連部屋訪問状態をOFF
  $WK_OTHER_UN = 0
INIT_EVENT_END

/************************************************************/
/*			予約スクリプト：エレベータから出てくる					*/
/************************************************************/
EVENT_START	ev_reserve_c11r0101
  _CALL player_out_anm
EVENT_END

/**************************************************************/
/*            受付1                                            */
/**************************************************************/
EVENT_START ev_c11r0101_reception01
  DEFINE_LOCAL AppSelectRec
  DEFINE_LOCAL Floor
  //「国連行く？」
  _BALLOONWIN_TALKOBJ_MSG( msg_reception01_01 )
  _YES_NO_WIN( SCWK_ANSWER )
  _BALLOONWIN_CLOSE()

  IF $SCWK_ANSWER == 0 THEN     //行く
    //「行き先えらんで」
    _BALLOONWIN_TALKOBJ_MSG( msg_reception01_01a )
    _BALLOONWIN_CLOSE()
    //選択アプリ    todo
    $AppSelectRec = 1   //国コードセット  @todo
    $Floor = 1          //フロアセット　@todo
    //データセット
    _UN_SET_COUNTRY_INFO( AppSelectRec, Floor )
    //「こちらへ」
    _BALLOONWIN_TALKOBJ_MSG( msg_reception01_02 )
    _BALLOONWIN_CLOSE()
    //アニメ
    _CALL player_in_anm
    //国連部屋訪問状態をON
    $WK_OTHER_UN = 1
    //予約スクリプト マップチェンジ後、エレベータから出てくるスクリプトを起動する
		_RESERVE_SCRIPT( SCRID_RESERVE_C11R0201 )
    //マップチェンジ
    _MAP_CHANGE( ZONE_ID_C11R0201, 10, 5, DIR_DOWN )
  ELSE
    //「またね」
    _BALLOONWIN_TALKOBJ_MSG( msg_reception01_03 )
    _LAST_KEYWAIT()
    _BALLOONWIN_CLOSE()
  ENDIF  

EVENT_END

/**************************************************************/
/*            受付2                                            */
/**************************************************************/
EVENT_START ev_c11r0101_reception02
  _EASY_BALLOONWIN_TALKOBJ_MSG( msg_reception02_01 )
EVENT_END

/**************************************************************/
/*            受付男性                                        */
/**************************************************************/
EVENT_START ev_c11r0101_reception03
  DEFINE_LOCAL npc_num

  //有効データ数を取得
  _UN_GET_VALID_DATA_NUM( npc_num )
  _NUMBER(11, npc_num, 2)     //11番に人数をセット（2桁）
  _EASY_BALLOONWIN_TALKOBJ_MSG( msg_reception03_01 )
EVENT_END

/**************************************************************/
/*            バックパッカー                                  */
/**************************************************************/
EVENT_START ev_c11r0101_backpackerm01
  _EASY_BALLOONWIN_TALKOBJ_MSG( msg_backpackerm_01 )
EVENT_END

/**************************************************************/
/*            男の子                                          */
/**************************************************************/
EVENT_START ev_c11r0101_boy01
  _EASY_BALLOONWIN_TALKOBJ_MSG( msg_babyboy1_01 )
EVENT_END

/**************************************************************/
/*            ダンサー                                        */
/**************************************************************/
EVENT_START ev_c11r0101_dancer01
  _EASY_BALLOONWIN_TALKOBJ_MSG( msg_dancer_01 )
EVENT_END

/**************************************************************/
/*            ガードマン                                      */
/**************************************************************/
EVENT_START ev_c11r0101_policeman01
  DEFINE_LOCAL	gardman_talk_msg
  SWITCH $WK_SCENE_C11GATE
  CASE 0 THEN     //ここにはこないけど
    $gardman_talk_msg = msg_policeman_01_01
  CASE 1 THEN     //初回
    $gardman_talk_msg = msg_policeman_01_01
  CASE 2 THEN     //1ヶ国
    $gardman_talk_msg = msg_policeman_01_02
  CASE 3 THEN     //3ヶ国
    $gardman_talk_msg = msg_policeman_01_03
  CASE 4 THEN     //6ヶ国
    $gardman_talk_msg = msg_policeman_01_04
  CASE 5 THEN     //10ヶ国
    $gardman_talk_msg = msg_policeman_01_05
  CASE 6 THEN     //15
    $gardman_talk_msg = msg_policeman_01_06
  CASE 7 THEN     //20
    $gardman_talk_msg = msg_policeman_01_07
  CASE 8 THEN     //30
    $gardman_talk_msg = msg_policeman_01_08
  CASE 9 THEN     //40
    $gardman_talk_msg = msg_policeman_01_09
  CASE 10 THEN     //50
    $gardman_talk_msg = msg_policeman_01_10
  CASE 11 THEN    //60
    $gardman_talk_msg = msg_policeman_01_11
  CASE 12 THEN    //70
    $gardman_talk_msg = msg_policeman_01_12
  CASE 13 THEN    //80
    $gardman_talk_msg = msg_policeman_01_13
  CASE 14 THEN    //90
    $gardman_talk_msg = msg_policeman_01_14
  CASE 15 THEN    //100
    $gardman_talk_msg = msg_policeman_01_15
  CASE 16 THEN    //150
    $gardman_talk_msg = msg_policeman_01_16
  DEFAULT  
    $gardman_talk_msg = msg_policeman_01_17
  ENDSWITCH

  //プレーヤー名をタグ3番にセット
  _PLAYER_NAME( 3 )
  _BALLOONWIN_OBJMSG(gardman_talk_msg, UN_ENTRANCE_POLICEMAN_01)
  _LAST_KEYWAIT()
  _BALLOONWIN_CLOSE()

EVENT_END

/**************************************************************/
/*            ガードマン     POSイベント                      */
/**************************************************************/
EVENT_START ev_c11r0101_pos01
  DEFINE_LOCAL	talk
  DEFINE_LOCAL	gardman_msg
  //シーンワークで分岐
  $talk = TRUE

  //ガードマン下向く
  _OBJ_ANIME( UN_ENTRANCE_POLICEMAN_01, anm_step_dir_down)
  _OBJ_ANIME_WAIT()
  //「こっちこいや」吹き出しを下に出す
  _BALLOONWIN_OBJMSG_POS(msg_policeman_01_00, UN_ENTRANCE_POLICEMAN_01, WIN_DOWN)
  _BALLOONWIN_CLOSE()

  //ガードマン左2歩下向き
  _OBJ_ANIME( UN_ENTRANCE_POLICEMAN_01, anm_move_left)
  //自機上に2歩
  _OBJ_ANIME( MMDL_ID_PLAYER, anm_move_up)
  _OBJ_ANIME_WAIT()

  SWITCH $WK_SCENE_C11GATE
  CASE 0 THEN     //初回
    $gardman_msg = msg_policeman_01_01
  CASE 1 THEN     //1ヶ国
    $gardman_msg = msg_policeman_01_02
  CASE 2 THEN     //3ヶ国
    $gardman_msg = msg_policeman_01_03
  CASE 3 THEN     //6ヶ国
    $gardman_msg = msg_policeman_01_04
  CASE 4 THEN     //10ヶ国
    $gardman_msg = msg_policeman_01_05
  CASE 5 THEN     //15
    $gardman_msg = msg_policeman_01_06
  CASE 6 THEN     //20
    $gardman_msg = msg_policeman_01_07
  CASE 7 THEN     //30
    $gardman_msg = msg_policeman_01_08
  CASE 8 THEN     //40
    $gardman_msg = msg_policeman_01_09
  CASE 9 THEN     //50
    $gardman_msg = msg_policeman_01_10
  CASE 10 THEN    //60
    $gardman_msg = msg_policeman_01_11
  CASE 11 THEN    //70
    $gardman_msg = msg_policeman_01_12
  CASE 12 THEN    //80
    $gardman_msg = msg_policeman_01_13
  CASE 13 THEN    //90
    $gardman_msg = msg_policeman_01_14
  CASE 14 THEN    //100
    $gardman_msg = msg_policeman_01_15
  CASE 15 THEN    //150
    $gardman_msg = msg_policeman_01_16
  CASE 16 THEN    //233
    $gardman_msg = msg_policeman_01_17
  DEFAULT
    $talk = FALSE
    //何もしない
  ENDSWITCH

  IF $talk == TRUE THEN
    //プレーヤー名をタグ3番にセット
    _PLAYER_NAME( 3 )
    _BALLOONWIN_OBJMSG(gardman_msg, UN_ENTRANCE_POLICEMAN_01)
    _LAST_KEYWAIT()
    _BALLOONWIN_CLOSE()
    $WK_SCENE_C11GATE += 1    //シーンワーク更新
  ENDIF

  //ガードマン戻って下向く
  _OBJ_ANIME( UN_ENTRANCE_POLICEMAN_01, anm_move_right)
  _OBJ_ANIME_WAIT()

  //POSイベント無効化
  $LOCALWORK1 = 0

EVENT_END



//アニメ
player_in_anm:
  //受付開ける
  _OBJ_ANIME( UN_ENTRANCE_RECEPTION_01, anm_reception_open )
	_OBJ_ANIME_WAIT()
  //自機移動
  _OBJ_ANIME( MMDL_ID_PLAYER, anm_move_elev )
	_OBJ_ANIME_WAIT()

  //エレベータアニメセットアップ
  _BMODEL_ANIME_CREATE( DOOR_ANIME_ID, SCR_BMID_DOOR, 14, 6 )
  //エレベータ開く
	_BMODEL_ANIME_SET( DOOR_ANIME_ID, SCR_BMANM_DOOR_OPEN )
	_BMODEL_ANIME_WAIT( DOOR_ANIME_ID )

  //自機エレベータに入る
  _OBJ_ANIME( MMDL_ID_PLAYER, anm_move_elev_in )
	_OBJ_ANIME_WAIT()

  //エレベータ閉まる
  _BMODEL_ANIME_SET( DOOR_ANIME_ID, SCR_BMANM_DOOR_CLOSE )
	_BMODEL_ANIME_WAIT( DOOR_ANIME_ID )
  //エレベータアニメクリーンアップ
	_BMODEL_ANIME_DELETE( DOOR_ANIME_ID )
_RET

player_out_anm:
   //エレベータアニメセットアップ
  _BMODEL_ANIME_CREATE( DOOR_ANIME_ID, SCR_BMID_DOOR, 14, 6 )
  //エレベータ開く
	_BMODEL_ANIME_SET( DOOR_ANIME_ID, SCR_BMANM_DOOR_OPEN )
	_BMODEL_ANIME_WAIT( DOOR_ANIME_ID )

  //自機エレベータから出てくる
  _OBJ_ANIME( MMDL_ID_PLAYER, anm_move_elev_out )
	_OBJ_ANIME_WAIT()

  //エレベータ閉まる
  _BMODEL_ANIME_SET( DOOR_ANIME_ID, SCR_BMANM_DOOR_CLOSE )
  //自機エントランスへ
  _OBJ_ANIME( MMDL_ID_PLAYER, anm_move_entrance )
	_OBJ_ANIME_WAIT()
	_BMODEL_ANIME_WAIT( DOOR_ANIME_ID )
  //エレベータアニメクリーンアップ
	_BMODEL_ANIME_DELETE( DOOR_ANIME_ID )
  
  //受付ふさぐ
  _OBJ_ANIME( UN_ENTRANCE_RECEPTION_01, anm_reception_close )
	_OBJ_ANIME_WAIT()
_RET


//--------------------------------------------------------------------
//受付開ける
//--------------------------------------------------------------------
_ANIME_LABEL	anm_reception_open
	_ANIME_DATA	AC_WALK_U_8F,1
  _ANIME_DATA	AC_WALK_L_8F,1
  _ANIME_DATA	AC_STAY_WALK_R_8F,1
	_ANIME_DATA	ACMD_END,0

//--------------------------------------------------------------------
//エレベーター前まで移動
//--------------------------------------------------------------------
_ANIME_LABEL	anm_move_elev
	_ANIME_DATA	AC_WALK_U_8F,5
	_ANIME_DATA	ACMD_END,0

//--------------------------------------------------------------------
//エレベーターにはいる
//--------------------------------------------------------------------
_ANIME_LABEL	anm_move_elev_in
	_ANIME_DATA	AC_WALK_U_8F,2
  _ANIME_DATA	AC_STAY_WALK_D_8F,1
	_ANIME_DATA	ACMD_END,0

//--------------------------------------------------------------------
//エレベーターからでてくる
//--------------------------------------------------------------------
_ANIME_LABEL	anm_move_elev_out
	_ANIME_DATA	AC_WALK_D_8F,2
	_ANIME_DATA	ACMD_END,0

//--------------------------------------------------------------------
//エントランスへ
//--------------------------------------------------------------------
_ANIME_LABEL	anm_move_entrance
	_ANIME_DATA	AC_WALK_D_8F,5
	_ANIME_DATA	ACMD_END,0

//--------------------------------------------------------------------
//受付閉める
//--------------------------------------------------------------------
_ANIME_LABEL	anm_reception_close
	_ANIME_DATA	AC_WALK_R_8F,1
  _ANIME_DATA	AC_WALK_D_8F,1
	_ANIME_DATA	ACMD_END,0


//POSイベント起動判定
check_posevt_start:
  DEFINE_LOCAL	chk_total
  DEFINE_LOCAL	chk_val

  //シーンワークで分岐
  SWITCH $WK_SCENE_C11GATE
  CASE 0 THEN     //初回
    $chk_val = 0
  CASE 1 THEN     //1ヶ国
    $chk_val = 1
  CASE 2 THEN     //3ヶ国
    $chk_val = 3
  CASE 3 THEN     //6ヶ国
    $chk_val = 6
  CASE 4 THEN     //10ヶ国
    $chk_val = 10
  CASE 5 THEN     //15
    $chk_val = 15
  CASE 6 THEN     //20
    $chk_val = 20
  CASE 7 THEN     //30
    $chk_val = 30
  CASE 8 THEN     //40
    $chk_val = 40
  CASE 9 THEN     //50
    $chk_val = 50
  CASE 10 THEN    //60
    $chk_val = 60
  CASE 11 THEN    //70
    $chk_val = 70
  CASE 12 THEN    //80
    $chk_val = 80
  CASE 13 THEN    //90
    $chk_val = 90
  CASE 14 THEN    //100
    $chk_val = 100
  CASE 15 THEN    //150
    $chk_val = 150
  CASE 16 THEN    //233
    $chk_val = 233
  DEFAULT
    //何もしないでリターン
    _RET
  ENDSWITCH

  //ヶ国数取得
  _UN_GET_COUNTRY_NUM( chk_total )
  //POS起動条件を満たす国数まで到達したか？
  IF $chk_total >= $chk_val THEN
    $LOCALWORK1 = 1   //POS起動
  ENDIF
_RET

_ANIME_LABEL	anm_step_dir_down
  _ANIME_DATA	AC_STAY_WALK_D_8F,1
	_ANIME_DATA	ACMD_END,0  

_ANIME_LABEL	anm_move_left
  _ANIME_DATA	AC_WALK_L_8F,2
  _ANIME_DATA	AC_STAY_WALK_D_8F,1
	_ANIME_DATA	ACMD_END,0

_ANIME_LABEL	anm_move_up
  _ANIME_DATA	AC_WALK_U_8F,2
	_ANIME_DATA	ACMD_END,0

_ANIME_LABEL	anm_move_right
  _ANIME_DATA	AC_WALK_R_8F,2
  _ANIME_DATA	AC_STAY_WALK_L_8F,1
	_ANIME_DATA	ACMD_END,0  
  


