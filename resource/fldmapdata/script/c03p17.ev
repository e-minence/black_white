//======================================================================
//                c03p17.ev
//
//    スクリプトファイル：c03p17用
//
//======================================================================

  .text

  .include  "scr_seq_def.h"
  .include  "msg_c03p17.h"
  .include  "c03p17.h"

//--------------------------------------------------------------
//               スクリプト本体
//--------------------------------------------------------------

//--------------------------------------------------------------
//               スクリプトテーブル
//--------------------------------------------------------------
_EVENT_DATA		ev_c03p17_badman_01
_EVENT_DATA   ev_zone_c03p17
_EVENT_DATA   ev_init_c03p17
_EVENT_DATA   ev_recover_c03p17
_EVENT_DATA		ev_c03p17_assistantm_01
_EVENT_DATA_END

//------------------------------注意--------------------------------//
//																	//
//	LOCALWORK0	まで使用しました。同マップ内の使用を禁止します。	//
//				LOCALWORK0⇒進化の石を貰うイベント					//
//------------------------------注意--------------------------------//

/**************************************************************/
/*            特殊スクリプト                               */
/**************************************************************/
INIT_EVENT_START  ev_zone_c03p17
{
  DEFINE_LOCAL  LWK_MONTH
  DEFINE_LOCAL  LWK_DAY
  _GET_DATE( LWK_MONTH, LWK_DAY )

//偶数の日は船が出現しない
//その状態をマップに入ったとき、LOCALWORK1に保存しておく
  SWITCH $LWK_DAY
  CASE 2,4,6,8,10,12,14,16,18,20,22,24,26,28,30 THEN
    $LOCALWORK1  = 1
  DEFAULT
    $LOCALWORK1  = 0
  ENDSWITCH
}
INIT_EVENT_END

/**************************************************************/
/**************************************************************/
INIT_EVENT_START  ev_init_c03p17
  IF $LOCALWORK1 == 1 THEN
    _BMODEL_CHANGE_VIEW_FLAG( SCR_BMID_EVENT, 12, 25, FALSE )
  ENDIF
INIT_EVENT_END

/**************************************************************/
/**************************************************************/
INIT_EVENT_START  ev_recover_c03p17
  IF $LOCALWORK1 == 1 THEN
    _BMODEL_CHANGE_VIEW_FLAG( SCR_BMID_EVENT, 12, 25, FALSE )
  ENDIF
INIT_EVENT_END



/**************************************************************/
/* 			BADMAN	C03P17									  */
/**************************************************************/
EVENT_START		ev_c03p17_badman_01
	_EASY_BALLOONWIN_TALKOBJ_MSG( msg_c03p17_badman_01 )
EVENT_END

/**************************************************************/
/* 			ASSISTANTM	C03P17									  */
/**************************************************************/
EVENT_START		ev_c03p17_assistantm_01
//ループに入る為、NOで初期化
$LOCALWORK0 = SCR_NO
	
	_TALK_OBJ_START()
	//いしを貰った場合
	IF FLAG_ON( FE_C03P17_ITEMGET_01 ) THEN
		_BALLOONWIN_OBJMSG_POS( msg_c03p17_assistantm_05,C03P17_ASSISTANTM_01,POS_DOWN )
		_LAST_KEYWAIT()
		_MSGWIN_CLOSE()
	//もらっていない場合
	ELSE

		DEFINE_LOCAL	LWK_SELECT
		//ブロッコ　スチック　ファウンの３びきは
		_BALLOONWIN_OBJMSG_POS( msg_c03p17_assistantm_01,C03P17_ASSISTANTM_01,POS_DOWN )
			
		WHILE $LOCALWORK0 == SCR_NO		
			//三択表示・ブロッコ　スチック　ファウン
	   		_BMPMENU_INIT_RIGHT( MENU_X1, MENU_Y1, 0, 1, SCWK_ANSWER )
		
			_BMPMENU_MAKE_LIST( msg_c03p17_select_01, 0 )	//『ブロッコ』
			_BMPMENU_MAKE_LIST( msg_c03p17_select_02, 1 )	//『スチック』
			_BMPMENU_MAKE_LIST( msg_c03p17_select_03, 2 )	//『ファウン』
			// メニュー表示
			_BMPMENU_START()

			//Bボタンでキャンセルした場合、ループを抜ける 
			$LOCALWORK0 = SCR_YES
			_MSGWIN_CLOSE()
						
			//『ブロッコ』
			IF $SCWK_ANSWER   == 0 THEN
				_BALLOONWIN_OBJMSG_POS(msg_c03p17_assistantm_06,C03P17_ASSISTANTM_01,POS_DOWN)
				_YES_NO_WIN(LOCALWORK0)
				_MSGWIN_CLOSE()
			//『スチック』
			ELSIF $SCWK_ANSWER   == 1 THEN
				_BALLOONWIN_OBJMSG_POS(msg_c03p17_assistantm_07,C03P17_ASSISTANTM_01,POS_DOWN)
				_YES_NO_WIN(LOCALWORK0)
				_MSGWIN_CLOSE()			
			//『ファウン』
			ELSIF $SCWK_ANSWER   == 2 THEN
				_BALLOONWIN_OBJMSG_POS(msg_c03p17_assistantm_08,C03P17_ASSISTANTM_01,POS_DOWN)
				_YES_NO_WIN(LOCALWORK0)
				_MSGWIN_CLOSE()
			ENDIF
			
			IF$LOCALWORK0 == SCR_NO THEN
				_TIME_WAIT(16)
				_BALLOONWIN_OBJMSG_POS(msg_c03p17_assistantm_09,C03P17_ASSISTANTM_01,POS_DOWN)
			ENDIF
		ENDWHILE		
				
		SWITCH $SCWK_ANSWER
		CASE 0 THEN
			_BALLOONWIN_OBJMSG_POS( msg_c03p17_assistantm_02,C03P17_ASSISTANTM_01,POS_DOWN)
			_MSGWIN_CLOSE()
					
			_ITEM_EVENT_KEYWAIT( ITEM_RIIHUNOISI, 1 )
			_FLAG_SET( FE_C03P17_ITEMGET_01 )

			_BALLOONWIN_OBJMSG_POS( msg_c03p17_assistantm_05,C03P17_ASSISTANTM_01,POS_DOWN )
			_LAST_KEYWAIT()
			_MSGWIN_CLOSE()
					
		CASE 1 THEN
			_BALLOONWIN_OBJMSG_POS( msg_c03p17_assistantm_03,C03P17_ASSISTANTM_01,POS_DOWN )
			_MSGWIN_CLOSE()
					
			_ITEM_EVENT_KEYWAIT( ITEM_HONOONOISI, 1 )
			_FLAG_SET( FE_C03P17_ITEMGET_01 )

			_BALLOONWIN_OBJMSG_POS( msg_c03p17_assistantm_05,C03P17_ASSISTANTM_01,POS_DOWN )
			_LAST_KEYWAIT()
			_MSGWIN_CLOSE()
		CASE 2 THEN
			_BALLOONWIN_OBJMSG_POS( msg_c03p17_assistantm_04,C03P17_ASSISTANTM_01,POS_DOWN )
			_MSGWIN_CLOSE()
					
			_ITEM_EVENT_KEYWAIT( ITEM_MIZUNOISI, 1 )
			_FLAG_SET( FE_C03P17_ITEMGET_01 )

			_BALLOONWIN_OBJMSG_POS( msg_c03p17_assistantm_05,C03P17_ASSISTANTM_01,POS_DOWN )
			_LAST_KEYWAIT()
			_MSGWIN_CLOSE()
		ENDSWITCH
	ENDIF
EVENT_END
