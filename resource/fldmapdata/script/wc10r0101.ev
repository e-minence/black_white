
//======================================================================
//                wc10r0101.ev
//
//    スクリプトファイル：wc10r0101用
//
//======================================================================

  .text

  .include  "scr_seq_def.h"
  .include  "msg_wc10r0101.h"
  .include  "wc10r0101.h"
  .include "../../../prog/src/field/scrcmd_wfbc_define.h"

//--------------------------------------------------------------
//               スクリプト本体
//--------------------------------------------------------------

//--------------------------------------------------------------
//               スクリプトテーブル
//--------------------------------------------------------------
_EVENT_DATA    ev_wc10r0101_oldman1
_EVENT_DATA    ev_wc10r0101_boy2_01
_EVENT_DATA    ev_wc10r0101_boy3_01
_EVENT_DATA    ev_wc10r0101_middlewoman1_01
_EVENT_DATA    ev_wc10r0101_boy3_02
_EVENT_DATA    ev_wc10r0101_woman3_01
_EVENT_DATA_END

//------------------------------注意--------------------------------//
//																	//
//	LOCALWORK15を使用しました。同マップ内の使用を禁止します。		//
//				LOCALWORK15⇒ランダムに選ばれるアイテム結果格納箇所	//
//																	//
//------------------------------------------------------------------//

/**************************************************************/
/* ホワイトフォレスト：村長                                      */
/**************************************************************/
EVENT_START   ev_wc10r0101_oldman1

	_TALK_OBJ_START()

	//今日既にイベントを終えたかチェック
	IF FLAG_ON( TMFLG_WC10R0101_OLDMAN ) THEN
	
		_BALLOONWIN_OBJMSG( msg_wc10r0101_oldman1_06 , WC10R0101_OLDMAN1 )
		_BALLOONWIN_CLOSE()
		
	//イベントを終えていない
	ELSE

		//初回話しかけかチェック
		IF FLAG_OFF( FE_WC10R0101_OLDMAN ) THEN
		
			_BALLOONWIN_OBJMSG( msg_wc10r0101_oldman1_01 , WC10R0101_OLDMAN1 )
			
			//==================================//
			
			//初回話しかけ完了
			_FLAG_SET ( FE_WC10R0101_OLDMAN )
			
			//==================================//
		ELSE
		
		ENDIF
		
		//現在のWFの人口を取得
		DEFINE_LOCAL LWK_POPULATION
		_WFBC_GET_DATA( WFBC_GET_PARAM_PEOPLE_NUM, LWK_POPULATION )
		
		//人口が一人以上かチェック
		
		//達している
		IF $LWK_POPULATION >= 1 THEN
			
			//目標ポケモンを取得
			DEFINE_LOCAL LWK_TARGETPOKE
			_WFBC_GET_DATA( WFBC_GET_PARAM_WF_POKE, LWK_TARGETPOKE )
			//指定IDに目標ポケモン名を格納
			_MONS_NAME (  0 , LWK_TARGETPOKE  ) 
			
			//村長：最近このあたりで（目標ポケモン）を見かける
			_BALLOONWIN_OBJMSG( msg_wc10r0101_oldman1_02 , WC10R0101_OLDMAN1 )
			
			//手持ちに条件に一致するポケモンがいるかチェック
			DEFINE_LOCAL LWK_NO_MATCH
			DEFINE_LOCAL LWK_DAY_MATCH
			DEFINE_LOCAL LWK_POSID
			_WFBC_CHECK_WF_TARGETPOKE( LWK_NO_MATCH, LWK_DAY_MATCH, LWK_POSID ) 
			
			//1匹以上いる
			IF $LWK_NO_MATCH == TRUE THEN
				
				//村長：おお！それは（目標ポケモン）！
				_BALLOONWIN_OBJMSG( msg_wc10r0101_oldman1_03 , WC10R0101_OLDMAN1 )
				
				//日付チェック
				IF $LWK_DAY_MATCH == TRUE THEN
					
					//村長：しかも今日捕まえたんだ！
					_BALLOONWIN_OBJMSG( msg_wc10r0101_oldman1_04 , WC10R0101_OLDMAN1 )
					_BALLOONWIN_CLOSE() 
					
					//乱数で0〜4のうちのひとつを選んで格納する
					_GET_RND( LOCALWORK15,5 )
					
					//乱数の結果によって入手するアイテムを変更
					IF $LOCALWORK15 == 0 THEN
						_ITEM_EVENT_KEYWAIT ( ITEM_MOMONNOMI , 1)
					ELSIF $LOCALWORK15 == 1 THEN
						_ITEM_EVENT_KEYWAIT ( ITEM_BURIINOMI , 1)
					ELSIF $LOCALWORK15 == 2 THEN
						_ITEM_EVENT_KEYWAIT ( ITEM_RAMUNOMI , 1)
					ELSIF $LOCALWORK15 == 3 THEN
						_ITEM_EVENT_KEYWAIT ( ITEM_KAGONOMI , 1)
					ELSE
						_ITEM_EVENT_KEYWAIT ( ITEM_HIMERINOMI , 1)
					ENDIF
				
					//村長：また来てね
					_BALLOONWIN_OBJMSG( msg_wc10r0101_oldman1_05 , WC10R0101_OLDMAN1 )
					_LAST_KEYWAIT()
					_BALLOONWIN_CLOSE() 
					
					//==================================//
			
					//本日のイベント終了
					_FLAG_SET ( TMFLG_WC10R0101_OLDMAN )
					
					//==================================//
				
				//捕まえた日が今日ではなかった
				ELSE
				
					//村長：しかし今日捕まえたやつじゃないな
					_BALLOONWIN_OBJMSG( msg_wc10r0101_oldman1_09 , WC10R0101_OLDMAN1 )
					_LAST_KEYWAIT()
					_BALLOONWIN_CLOSE() 
				
				ENDIF
			
			//目標ポケモンが０匹
			ELSE
			
				//村長：見つけたら見せてね
				_BALLOONWIN_OBJMSG( msg_wc10r0101_oldman1_08 , WC10R0101_OLDMAN1 )
				_LAST_KEYWAIT()
				_BALLOONWIN_CLOSE() 
				
			ENDIF
		
		//人口は０人
		ELSE
		
			//村長：最近はポケモンがいない
			_BALLOONWIN_OBJMSG( msg_wc10r0101_oldman1_07 , WC10R0101_OLDMAN1 )
			_LAST_KEYWAIT()
			_BALLOONWIN_CLOSE() 
		
		ENDIF
	
	ENDIF

EVENT_END

/**************************************************************/
/* ホワイトフォレスト：トリプルバトル                        */
/**************************************************************/
EVENT_START   ev_wc10r0101_boy3_02
{
	_TALK_OBJ_START()
	
	//まだ勝利していない
	IF FLAG_OFF( FE_WC10R0101_BTL_01 ) THEN
		_TALK_OBJ_START()
		_BALLOONWIN_OBJMSG_POS( msg_wc10r0101_boy3a_01 , WC10R0101_BOY3_02 , POS_DEFAULT )
		_YES_NO_WIN( SCWK_ANSWER )
		
		//はい
		IF $SCWK_ANSWER == 0 THEN
		
			//手持ちポケモンチェック
			DEFINE_LOCAL LCW_POKECOUNTB
			_GET_PARTY_POKE_COUNT( LCW_POKECOUNTB , POKECOUNT_MODE_BATTLE_ENABLE )
			
			//戦えるポケモンが３匹未満
			IF $LCW_POKECOUNTB < 3 THEN
			
				_BALLOONWIN_OBJMSG(msg_wc10r0101_boy3a_01a,WC10R0101_BOY3_02)
				_LAST_KEYWAIT()
				_BALLOONWIN_CLOSE()
			
			//3匹以上
			ELSE
			
				_BALLOONWIN_CLOSE()
			
				//ローテーションバトルセット
				_TRAINER_BTL_SET( TRID_SCHOOLB_10, 0 )
				//結果チェック
				_TRAINER_LOSE_CHECK( SCWK_ANSWER )
				IF $SCWK_ANSWER == SCR_BATTLE_RESULT_WIN THEN
					_TRAINER_WIN()
				ELSE
					_TRAINER_LOSE()
				ENDIF
				
					//======================================//
						
						_FLAG_SET( FE_WC10R0101_BTL_01 )
						
					//======================================//
			ENDIF
		
		//いいえ
		ELSE
			_BALLOONWIN_OBJMSG_POS( msg_wc10r0101_boy3a_02 , WC10R0101_BOY3_02 , POS_DEFAULT )
			_LAST_KEYWAIT()
			_BALLOONWIN_CLOSE()
	
		ENDIF
	
	//既に戦闘済み
	ELSE
		_EASY_BALLOONWIN_TALKOBJ_MSG( msg_wc10r0101_boy3a_03 )
	ENDIF
}
EVENT_END

/**************************************************************/
/* ホワイトフォレスト：トリプルバトル                        */
/**************************************************************/
EVENT_START   ev_wc10r0101_woman3_01
{
	_TALK_OBJ_START()
	
	//まだ勝利していない
	IF FLAG_OFF( FE_WC10R0101_BTL_02 ) THEN
		_TALK_OBJ_START()
		_BALLOONWIN_OBJMSG_POS( msg_wc10r0101_woman3_01 , WC10R0101_WOMAN3_01 , POS_DEFAULT )
		_YES_NO_WIN( SCWK_ANSWER )
		
		//はい
		IF $SCWK_ANSWER == 0 THEN
		
			//手持ちポケモンチェック
			DEFINE_LOCAL LCW_POKECOUNTB
			_GET_PARTY_POKE_COUNT( LCW_POKECOUNTB , POKECOUNT_MODE_BATTLE_ENABLE )
			
			//戦えるポケモンが３匹未満
			IF $LCW_POKECOUNTB < 3 THEN
			
				_BALLOONWIN_OBJMSG(msg_wc10r0101_woman3_01a,WC10R0101_WOMAN3_01)
				_LAST_KEYWAIT()
				_BALLOONWIN_CLOSE()
			
			//3匹以上
			ELSE
			
				_BALLOONWIN_CLOSE()
			
				//ローテーションバトルセット
				_TRAINER_BTL_SET( TRID_CHILDCARE_04, 0 )
				//結果チェック
				_TRAINER_LOSE_CHECK( SCWK_ANSWER )
				IF $SCWK_ANSWER == SCR_BATTLE_RESULT_WIN THEN
					_TRAINER_WIN()
				ELSE
					_TRAINER_LOSE()
				ENDIF
				
					//======================================//
						
						_FLAG_SET( FE_WC10R0101_BTL_02 )
						
					//======================================//
			ENDIF
			
		//いいえ
		ELSE
			_BALLOONWIN_OBJMSG_POS( msg_wc10r0101_woman3_02 , WC10R0101_WOMAN3_01 , POS_DEFAULT )
			_LAST_KEYWAIT()
			_BALLOONWIN_CLOSE()
	
		ENDIF
	
	//既に戦闘済み
	ELSE
		_EASY_BALLOONWIN_TALKOBJ_MSG( msg_wc10r0101_woman3_03 )
	ENDIF
}
EVENT_END

/**************************************************************/
/* 一言メッセージ：BOY2                                      */
/**************************************************************/
EVENT_START   ev_wc10r0101_boy2_01
	_EASY_BALLOONWIN_TALKOBJ_MSG( msg_wc10r0101_boy2_01 )
EVENT_END

/**************************************************************/
/* 一言メッセージ：BOY3                                      */
/**************************************************************/
EVENT_START   ev_wc10r0101_boy3_01
	_EASY_BALLOONWIN_TALKOBJ_MSG( msg_wc10r0101_boy3_01 )
EVENT_END

/**************************************************************/
/* 一言メッセージ：MIDDLEWOMAN1                                */
/**************************************************************/
EVENT_START   ev_wc10r0101_middlewoman1_01
	_EASY_BALLOONWIN_TALKOBJ_MSG( msg_wc10r0101_middlewoman2_01 )
EVENT_END